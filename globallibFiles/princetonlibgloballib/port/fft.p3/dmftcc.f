      SUBROUTINE DMFTCC(N,NNS,A,B,IAB,JAB,C,D,ICD,JCD,IFX,T,SGN)
C
C   THIS SUBROUTINE COMPUTES MULTIPLE DISCRETE FOURIER TRANSFORMS OF
C   NNS (DOUBLE) COMPLEX INPUT VECTORS (A + I*B) OF LENGTH N. THE OUTPUT
C   IS ALSO A SET OF COMPLEX VECTORS (C + I*D). THAT IS, IF X(J,K)
C   ARE A SET OF COMPLEX INPUT VECTORS (K COUNTS VECTORS, K = 1,NNS,
C   WHOSE ELEMENTS ARE X(J,K), J = 1,N) WITH REAL AND IMAGINARY PARTS
C   A(J,K) AND B(J,K) RESPECTIVELY, (X(J,K) = A(J,K) + I*B(J,K)),
C   THE OUTPUT FROM DMFTCC IS GIVEN FOR L = 1,N BY
C
C        Y(L,K) =   SUM DEXP(+-2*PI*I*(J-1)*(L-1)/N)*X(J,K)
C                  J=1,N
C
C   WHERE Y(L,K) = C(L,K) + I*D(L,K) AND K = 1,NNS, AND THE SIGN OF THE
C   OF THE EXPONENTIAL IS THAT OF PARAMETER SGN. THAT IS, DMFTCC
C   COMPUTES NNS (K = 1,NNS) REAL DISCRETE FOURIER TRANSFORMS OF
C   LENGTH N (L = 1,N) SIMULTANEOUSLY.
C
C INPUT PARAMETERS -
C
C   N     - THE NUMBER OF ELEMENTS IN EACH VECTOR WHOSE FOURIER
C           TRANSFORM IS DESIRED. THIS PARAMETER MUST BE GREATER
C           THAN 1.D0
C
C   NNS   - THE NUMBER OF VECTORS WHOSE FOURIER TRANSFORM IS
C           DESIRED - NNS MUST BE GREATER THAN 0.D0
C
C   A     - A DOUBLE PRECISION ARRAY CONTAINING THE REAL PARTS OF THE
C           INPUT VECTORS. THE SPACING BETWEEN EACH ELEMENT OF EVERY
C           VECTOR IS IAB, WHILE THE SPACING BETWEEN VECTORS IS
C           JAB. USUALLY, THE COLUMNS OF A WILL BE DISTINCT INPUT
C           VECTORS - ALTHOUGH INTERCHANGING THE IAB AND JAB
C           PARAMETERS WILL AFFECT A TRANSPOSITION.
C
C   B     - A DOUBLE PRECISION ARRAY CONTAINING THE IMAGINARY PARTS OF
C           THE INPUT VECTORS. THE SPACING BETWEEN EACH ELEMENT OF EVERY
C           VECTOR IS IAB, WHILE THE SPACING BETWEEN VECTORS IS
C           JAB. SEE THE DISCUSSION OF ARRAY A ABOVE
C
C   IAB   - THE SPACING BETWEEN ELEMENTS WITHIN EACH INPUT VECTOR OF
C           REAL PARTS (COLUMNS OF A), AND THE SPACING BETWEEN THE
C           ELEMENTS WITHIN EACH VECTOR OF IMAGINARY PARTS (COLUMNS
C           OF B).
C
C   JAB   - THE SPACING BETWEEN INPUT VECTORS OF REAL PARTS (SPACING
C           BETWEEN ROW ELEMENTS OF A), AND THE SPACING BETWEEN
C           INPUT VECTORS OF IMAGINARY PARTS (SPACING BETWEEN ROW
C           ELEMENTS OF B).
C
C   C     - A DOUBLE PRECISION ARRAY CONTAINING THE REAL PARTS OF THE
C           OUTPUT VECTORS. THE SPACING BETWEEN EACH ELEMENT OF EVERY
C           VECTOR IS ICD, WHILE THE SPACING BETWEEN VECTORS IS
C           JCD. SEE THE DISCUSSION ABOVE FOR THE INPUT ARRAY A.
C
C   D     - A DOUBLE PRECISION ARRAY CONTAINING THE IMAGINARY PARTS OF
C           THE OUTPUT VECTORS. THE SPACING BETWEEN EACH ELEMENT OF
C           EVERY VECTOR IS ICD, WHILE THE SPACING BETWEEN VECTORS IS
C           JCD. SEE THE DISCUSSION ABOVE FOR THE INPUT ARRAY A.
C
C   ICD   - THE SPACING BETWEEN ELEMENTS WITHIN EACH OUTPUT VECTOR OF
C           REAL PARTS (COLUMNS OF C), AND THE SPACING BETWEEN THE
C           ELEMENTS WITHIN EACH VECTOR OF IMAGINARY PARTS (COLUMNS
C           OF D).
C
C   JCD   - THE SPACING BETWEEN OUTPUT VECTORS OF REAL PARTS (SPACING
C           BETWEEN ROW ELEMENTS OF C), AND THE SPACING BETWEEN
C           OUTPUT VECTORS OF IMAGINARY PARTS (SPACING BETWEEN ROW
C           ELEMENTS OF D).
C
C   IFX   - AN INTEGER ARRAY OF LENGTH 25 IN WHICH THE FACTORS OF
C           THE INTEGER PARAMETER N WERE SAVED BY THE INITIALIZATION
C           SUBROUTINE DMFTCI. THE ELEMENTS OF IFX ARE
C                      IFX(1) = N
C                      IFX(2) = M, THE NUMBER OF FACTORS OF N
C                      IFX(3) THROUGH IFX(M+2) ARE THE FACTORS OF N
C
C   T     - A DOUBLE PRECISION ARRAY OF LENGTH N (N EVEN) OR N+2 (N ODD)
C           WHICH CONTAINS THE TRIGONOMETRIC TABLES COMPUTED BY THE
C           INITIALIZATION SUBROUTINE DMFTCI. THE ELEMENTS OF T ARE
C                      T(2*(K-1)+1) = COS(2*PI*(K-1)/N)
C                      T(2*(K-1)+2) = SIN(2*PI*(K-1)/N)
C           WHERE K = 1,N.
C
C    SGN  - A DOUBLE PRECISION PARAMETER WHICH MUST BE EITHER +1.0D0
C           OR -1.0D0.  THIS PARAMETER DETERMINES THE SIGN OF THE
C           ARGUMENT IN THE EXPONENTIAL DEFINING THE DISCRETE FOURIER
C           TRANSFORM.  (SEE ABOVE EXPRESSION FOR Y(L,K)) IF SGN IS
C           NEITHER 1.0D0 NOR -1.0D0 A FATAL ERROR RESULTS.
C
C   WRITTEN BY - W. P. PETERSEN, BELL LABORATORIES,
C               MURRAY HILL, N. J. , 1 SEPT. 1983D0
C
C
      COMMON /CSTAK/DSTAK(500)
      DOUBLE PRECISION DSTAK
C
      EXTERNAL ISTKGT
      INTEGER ISTKGT
      DOUBLE PRECISION A(1),B(1),C(1),D(1)
      DOUBLE PRECISION T(2,1),W(1)
      DOUBLE PRECISION SGN
C/6S
C     INTEGER IFX(1)
C/7S
      INTEGER IFX(*)
C/
      INTEGER I,IAB,ICD,IFAC,IW,IWN,JAB,JCD,LA,N,N2,NF2,NFX,NNS,NWK
      LOGICAL ITGLE
      EQUIVALENCE (W(1),DSTAK(1))
C
C   INPUT PARAMETER CHECKS
C
C/6S
C     IF (N .LT. 0 .OR. NNS .LT. 0)
C    1   CALL SETERR(33HDMFTCC - N .LT. 0 .OR. NNS .LT. 0,33,3,2)
C/7S
      IF (N .LT. 0 .OR. NNS .LT. 0)
     1   CALL SETERR('DMFTCC - N .LT. 0 .OR. NNS .LT. 0',33,3,2)
C/
      IF (N .EQ. 0) GO TO 999
      IF (NNS .EQ. 0) GO TO 999
C/6S
C     IF(N.NE.IFX(1))
C    1   CALL SETERR(36HDMFTCC - IFX INITIALIZED INCORRECTLY,36,1,2)
C     IF(DABS(SGN).NE.1.0D0)
C    1   CALL SETERR(38HDMFTCC - SIGN PARAMETER .NE. 1. OR -1.,38,2,2)
C/7S
      IF(N.NE.IFX(1))
     1   CALL SETERR('DMFTCC - IFX INITIALIZED INCORRECTLY',36,1,2)
      IF(DABS(SGN).NE.1.0D0)
     1   CALL SETERR('DMFTCC - SIGN PARAMETER .NE. 1. OR -1.',38,2,2)
C/
C
      IF (N .GT. 1) GO TO 10
         CALL DM88FT(N, NNS, A, B, IAB, JAB, C, D, ICD, JCD)
         GO TO 999
 10   IFAC = IFX(3)
      LA   = 1
      NFX  = IFX(2)
      NWK  = 2*N*NNS
      N2   = 2*N
C
C   GET WORK SPACE
C
      IW = ISTKGT(NWK,4)
      IWN = IW + N
C
C   FIRST PASS THROUGH DATA -
C      IF NFX (THE NUMBER OF FACTORS) IS ODD, COPY INTO WORK SPACE TO
C      ALLOW INPUT AND OUTPUT TO COINCIDE.  LOGICAL VARIABLE ITGLE IS
C      SET BY NFX BEING EVEN/ODD.
C
      NF2  = NFX/2
      IF(NFX .NE. NF2+NF2) GO TO 20
C
C        *** NFX IS EVEN ***
C
         CALL DM1FT(A,B,W(IW),W(IWN),T,1,
     1             IAB,1,JAB,N2,NNS,N,IFAC,LA,SGN)
         ITGLE = .TRUE.
         GO TO 30
C
 20   CALL DM88FT(N,NNS,A,B,IAB,JAB,W(IW),W(IWN),1,N2)
      CALL DM1FT(W(IW),W(IWN),C,D,T,1,1,ICD,N2,JCD,NNS,N,IFAC,LA,SGN)
      IF (NFX .EQ. 1) GO TO 60
      ITGLE = .FALSE.
C
C   *** MAIN LOOP ***
C
 30   CONTINUE
      LA = LA*IFAC
C
      DO 50 I = 2,NFX
         IFAC = IFX(I+2)
         IF(ITGLE) GO TO 40
            CALL DM1FT(C,D,W(IW),W(IWN),T,1,
     1                ICD,1,JCD,N2,NNS,N,IFAC,LA,SGN)
            LA = LA*IFAC
            ITGLE = .TRUE.
            GO TO 50
 40      CONTINUE
            CALL DM1FT(W(IW),W(IWN),C,D,T,1,
     1                1,ICD,N2,JCD,NNS,N,IFAC,LA,SGN)
            LA = LA*IFAC
            ITGLE = .FALSE.
C           ENDIF
 50   CONTINUE
C
 60   CALL ISTKRL(1)
C
 999  RETURN
      END
