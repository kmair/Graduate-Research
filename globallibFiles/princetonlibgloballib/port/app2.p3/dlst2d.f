      SUBROUTINE DLST2D(MDIM,M,N,A,UD)
C
C  TO OBTAIN THE Q*U DECOMPOSITION OF THE MATRIX A USING HOUSEHOLDER
C  TRANSFORMATIONS.
C
C  INPUT
C
C    MDIM - THE DIMENSIONED COLUMN LENGTH OF A
C    M    - THE NUMBER OF ROWS IN A.
C    N    - THE NUMBER OF COLUMNS IN A. N.LE.M IS ASSUMED.
C    A    - THE MATRIX A.
C
C  OUTPUT
C
C    A  - THE DECOMPOSED MATRIX.
C         LET V(J)(I)=0      FOR I=1,...,J-1
C         AND V(J)(I)=A(I,J) FOR I=J,...,M, THEN
C
C             Q = PRODUCT(J=1,...,N)(I-BETA(J)*V(J)*V(J)-TRANSPOSE)
C
C         WHERE BETA(J)=1/(UD(J)*ABS(A(J,J)))
C         A(I,J) FOR I.LT.J GIVES THE OFF-DIAGONAL ELEMENTS OF U.
C    UD - THE DIAGONAL OF U.
C
C  SCRATCH SPACE ALLOCATED - NONE.
C
C  ERROR STATES -
C
C    1 - MDIM.LT.M.
C    2 - N.LT.1.
C    3 - N.GT.M.
C    4 - A IS RANK-DEFICIENT. (RECOVERABLE)
C
C  P.A. BUSINGER, NUM. MATH. 7, 269-276(1965).
C
      DOUBLE PRECISION A(MDIM,N),UD(N)
C
      DOUBLE PRECISION S,BE,DSQRT
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (MDIM.LT.M) CALL SETERR(18HDLST2D - MDIM.LT.M,18,1,2)
C     IF (N.LT.1) CALL SETERR(15HDLST2D - N.LT.1,15,2,2)
C     IF (N.GT.M) CALL SETERR(15HDLST2D - N.GT.M,15,3,2)
C/7S
      IF (MDIM.LT.M) CALL SETERR('DLST2D - MDIM.LT.M',18,1,2)
      IF (N.LT.1) CALL SETERR('DLST2D - N.LT.1',15,2,2)
      IF (N.GT.M) CALL SETERR('DLST2D - N.GT.M',15,3,2)
C/
C
      DO 30 L=1,N
         S=0.D0
         DO 10 I=L,M
 10         S=S+A(I,L)**2
         IF (S.EQ.0.0D0) GO TO 40
         UD(L)=DSQRT(S)
         IF (A(L,L).GE.0.0D0) UD(L)=-UD(L)
         BE=S-A(L,L)*UD(L)
         A(L,L)=A(L,L)-UD(L)
         IF (L.EQ.N) GO TO 50
         LP1=L+1
         DO 30 J=LP1,N
            S=0.D0
            DO 20 I=L,M
 20            S=S+A(I,L)*A(I,J)
            S=S/BE
            DO 30 I=L,M
 30            A(I,J)=A(I,J)-S*A(I,L)
      GO TO 50
C
C ... RANK-DEFICIENT MATRIX.
C
C/6S
C40   CALL SETERR(28HDLST2D - A IS RANK-DEFICIENT,28,4,1)
C/7S
 40   CALL SETERR('DLST2D - A IS RANK-DEFICIENT',28,4,1)
C/
C
 50   RETURN
      END
