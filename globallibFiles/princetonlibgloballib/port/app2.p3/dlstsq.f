      SUBROUTINE DLSTSQ(MDIM,NDIM,M,N,A,B,NB,X)
C
C  LEAST SQUARES SOLUTION TO A SYSTEM OF LINEAR ALGEBRAIC
C  EQUATIONS WITH M BY N COEFFICIENT MATRIX A AND RIGHT
C  HAND SIDES B(*,J), J=1,...,NB. 1.LE.N.LE.M IS ASSUMED.
C
C  METHOD - QR DECOMPOSITION OF A BY HOUSEHOLDER TRANSFORMATIONS.
C
C  INPUT
C
C    MDIM - THE DIMENSIONED COLUMN LENGTH OF A AND B.
C    NDIM - THE DIMENSIONED COLUMN LENGTH OF X.
C    M    - THE NUMBER OF EQUATIONS
C    N    - THE NUMBER OF UNKNOWNS. N.LE.M IS ASSUMED.
C    A    - THE MATRIX.
C    B    - THE RIGHT-HAND SIDES.
C    NB   - THE NUMBER OF RIGHT-HAND SIDES B.
C
C  OUTPUT
C
C    A - CLOBBERED.
C    B - CLOBBERED.
C        SQRT(SUM(I=N+1,M)(B(I,J)**2)) IS THE L2 NORM OF THE RESIDUAL
C        IN THE SOLUTION FOR THE J-TH RIGHT-HAND SIDE, J=1,...,NB.
C    X - THE SOLUTION VECTORS. X=B IS OK IF NDIM=MDIM.
C
C  SCRATCH SPACE ALLOCATED - N DOUBLE PRECISION WORDS.
C
C  ERROR STATES -
C
C    1 - MDIM.LT.M.
C    2 - NDIM.LT.N.
C    3 - N.LT.1.
C    4 - N.GT.M.
C    5 - NB.LT.1.
C    6 - WHEN X=B MUST HAVE NDIM=MDIM.
C    7 - A IS RANK DEFICIENT. (RECOVERABLE)
C
      DOUBLE PRECISION A(MDIM,N),B(MDIM,NB),X(NDIM,NB)
C
      COMMON /CSTAK/S
      DOUBLE PRECISION S(500)
      DOUBLE PRECISION SAVE
C
      CALL ENTER(1)
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (MDIM.LT.M) CALL SETERR(18HDLSTSQ - MDIM.LT.M,18,1,2)
C     IF (NDIM.LT.N) CALL SETERR(18HDLSTSQ - NDIM.LT.N,18,2,2)
C     IF (N.LT.1) CALL SETERR(15HDLSTSQ - N.LT.1,15,3,2)
C     IF (N.GT.M) CALL SETERR(15HDLSTSQ - N.GT.M,15,4,2)
C     IF (NB.LT.1) CALL SETERR(16HDLSTSQ - NB.LT.1,16,5,2)
C/7S
      IF (MDIM.LT.M) CALL SETERR('DLSTSQ - MDIM.LT.M',18,1,2)
      IF (NDIM.LT.N) CALL SETERR('DLSTSQ - NDIM.LT.N',18,2,2)
      IF (N.LT.1) CALL SETERR('DLSTSQ - N.LT.1',15,3,2)
      IF (N.GT.M) CALL SETERR('DLSTSQ - N.GT.M',15,4,2)
      IF (NB.LT.1) CALL SETERR('DLSTSQ - NB.LT.1',16,5,2)
C/
C
C ... MAKE SURE THAT IF X=B THEN NDIM=MDIM.
C
      SAVE=B(1,1)
      X(1,1)=+1.0D0
      B(1,1)=-1.0D0
C/6S
C     IF (X(1,1).EQ.B(1,1) .AND. NDIM.NE.MDIM) CALL SETERR
C    1   (37HDLSTSQ - WHEN X=B MUST HAVE NDIM=MDIM,37,6,2)
C/7S
      IF (X(1,1).EQ.B(1,1) .AND. NDIM.NE.MDIM) CALL SETERR
     1   ('DLSTSQ - WHEN X=B MUST HAVE NDIM=MDIM',37,6,2)
C/
      B(1,1)=SAVE
C
C ... GET THE QR DECOMPOSITION OF A.
C
      IUD=ISTKGT(N,4)
      CALL DLST2D(MDIM,M,N,A,S(IUD))
C
C ... IF RANK(A)=N, SOLVE THE SYSTEMS ONE AT A TIME.
C
      IF (NERROR(NERR).NE.0) GO TO 20
      DO 10 J=1,NB
 10      CALL DLST2S(MDIM,M,N,A,S(IUD),B(1,J),X(1,J))
      GO TO 30
C
C ... RANK DEFICIENT MATRIX.
C
 20   CALL ERROFF
C/6S
C     CALL SETERR(28HDLSTSQ - A IS RANK DEFICIENT,28,7,1)
C/7S
      CALL SETERR('DLSTSQ - A IS RANK DEFICIENT',28,7,1)
C/
C
 30   CALL LEAVE
      RETURN
      END
