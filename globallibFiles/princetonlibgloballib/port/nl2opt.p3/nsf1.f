      SUBROUTINE  NSF1(N,L,X,X1,X2,T,C)
C
C     *************************************************
C     LINEAR LEAST SQUARES WITH ONE NONLINEAR PARAMETER
C     *************************************************
C
C     N   (INPUT)  NUMBER OF EQUATIONS
C     L   (INPUT)  NUMBER OF PARAMTERS (UNKNOWNS)
C     X   (OUTPUT)  SOLUTION TO NONLINEAR PARAMETER
C     X1  (INPUT)  ONE END OF INTERVAL CONTAINING X
C     X2  (INPUT)  OTHER END OF INTERVAL
C     T   (INPUT)  TOLERANCE
C     C   (OUTPUT)  SOLUTION VECTOR OF LINEAR PARAMETERS
C
      REAL RS(1000)
      INTEGER IS(1000)
      INTEGER L
      REAL C(L)
      REAL FMIN
      EXTERNAL L4ST1
      COMMON/L6NCB/NN,NP,IB,IY
      COMMON/CSTAK/R(1000)
      EQUIVALENCE (R(1),IS(1))
      EQUIVALENCE (R(1),RS(1))
C
CI DIMENSION ERROR CHECK
C
C/6S
C     IF(L .LT. 1) CALL SETERR(14H NSF1 - L.LT.1,14,1,2)
C     IF(L .GT. N) CALL SETERR(14H NSF1 - L.GT.N,14,2,2)
C/7S
      IF(L .LT. 1) CALL SETERR(' NSF1 - L.LT.1',14,1,2)
      IF(L .GT. N) CALL SETERR(' NSF1 - L.GT.N',14,2,2)
C/
C
      NN=N
      NP=L
      IB=ISTKGT(N*L,3)
      IY=ISTKGT(N,3)
C
C SOLVE 1-DIM PROBLEM
C
      FRSD=FMIN(L4ST1,X,X1,X2,T)
      CALL GETAY(NN,NP,X,RS(IB),RS(IY))
C
C SOLVE REMAINING LINEAR LEAST-SQUARES PROBLEM
C
      CALL LSTSQ(NN,NP,NN,NP,RS(IB),RS(IY),1,C)
      CALL ISTKRL(2)
      RETURN
      END
