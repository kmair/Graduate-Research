      REAL FUNCTION  SMNSX(FUNCT, P, STEP, TOL, X)
C
C *** NELDER-MEAD SIMPLEX METHOD (PATTERN SEARCH) FOR UNCONSTRAINED
C *** MINIMIZATION, EASY-TO-USE VERSION.
C
      INTEGER P
      REAL STEP, TOL, X(P)
      REAL FUNCT
      EXTERNAL FUNCT
C
C *** PARAMETERS ***
C
C FUNC (INPUT)   EXTERNAL FUNCTION TO COMPUTE F(X)
C P    (INPUT)   PROBLEM DIMENSION
C STEP (IN/OUT)  ON INPUT, THE SIZE OF THE INITIAL STEPS (FROM WHICH
C                THE INITIAL SIMPLEX IS CONSTRUCTED.  ON OUTPUT,
C                THE MAXIMUM WIDTH OF THE FINAL SIMPLEX IN ANY
C                COORDINATE DIRECTION.
C TOL  (IN/OUT)  ON INPUT, THE CONVERGENCE TOLERANCE (ON THE STANDARD
C                DEVIATION OF THE FUNCTION VALUES AT THE SIMPLEX
C                VERTICES).  ON OUTPUT, TOL IS SET TO THIS STANDARD
C                DEVIATION.
C X    (OUTPUT)  THE BEST X VALUE FOUND, CORRESPONDING TO THE RETURN
C                VALUE FROM SMNSX
C
C
      REAL RSTAK(1000)
      COMMON /CSTAK/ RSTAK
C
      INTEGER ISTKGT
      REAL  M7NSX
      EXTERNAL ISTKGT, ISTKRL,  M7NSX
C
C *** LOCAL VARIABLES ***
C
      INTEGER DX1, I, IRC, ITMX, J, P1, S1, SIJ, XE1, XR1, Y1
      REAL A, B, T, ZERO
      DATA ZERO/0.E+0/
C
C *** BODY ***
C
      IF (P .LE. 0) GO TO 40
      IF (STEP .LE. ZERO) GO TO 50
      IF (TOL .LE. ZERO) GO TO 50
      DX1 = ISTKGT(P*(P+5)+1, 3)
      XE1 = DX1 + P
      XR1 = XE1 + P
      Y1 = XR1 + P
      P1 = P + 1
      S1 = Y1 + P1
      ITMX = 1000
C
C *** INITIALIZE S ***
C
      A = -STEP / FLOAT(P1)
      B = STEP + A
      SIJ = S1
      DO 20 I = 1, P1
         DO 10 J = 1, P
            T = X(J) + A
            IF (I .EQ. J) T = X(J) + B
            RSTAK(SIJ) = T
            SIJ = SIJ + 1
 10         CONTINUE
 20      CONTINUE
C
       SMNSX =  M7NSX(RSTAK(DX1), FUNCT, IRC, ITMX, P, P1, P, RSTAK(S1),
     1               TOL, X, RSTAK(XE1), RSTAK(XR1), RSTAK(Y1))
      GO TO (70, 30, 40, 60), IRC
C/6S
C30   CALL SETERR(43H SMNSX -- NO CONVERGENCE IN 1000 ITERATIONS, 43,
C    1            1, 1)
C/7S
 30   CALL SETERR(' SMNSX -- NO CONVERGENCE IN 1000 ITERATIONS', 43,
     1            1, 1)
C/
      GO TO 70
C/6S
C40   CALL SETERR(39H SMNSX -- NONPOSITIVE PROBLEM DIMENSION, 39, 2, 2)
C50   CALL SETERR(39H SMNSX CALLED WITH STEP OR TOL .LE. 0.0, 39, 4, 2)
C60   CALL SETERR(
C    1  60H SMNSX -- FUNCTION AT SIMPLEX CENTER EXCEEDS MAX AT VERTICES,
C    2            59, 3, 1)
C/7S
 40   CALL SETERR(' SMNSX -- NONPOSITIVE PROBLEM DIMENSION', 39, 2, 2)
 50   CALL SETERR(' SMNSX CALLED WITH STEP OR TOL .LE. 0.0', 39, 4, 2)
 60   CALL SETERR(
     1  ' SMNSX -- FUNCTION AT SIMPLEX CENTER EXCEEDS MAX AT VERTICES',
     2            59, 3, 1)
C/
C
C *** SET STEP TO THE MAX. SIMPLEX WIDTH IN ANY COORDINATE DIRECTION...
C
 70   STEP = ZERO
      DO 90 I = 1, P
         A = RSTAK(S1)
         B = A
         SIJ = S1
         S1 = S1 + 1
         DO 80 J = 2, P1
            SIJ = SIJ + P
            T = RSTAK(SIJ)
            IF (A .GT. T) A = T
            IF (B .LT. T) B = T
 80         CONTINUE
         STEP = AMAX1(STEP, B-A)
 90      CONTINUE
C
      CALL ISTKRL(1)
 999  RETURN
C *** LAST LINE OF  SMNSX FOLLOWS ***
      END
