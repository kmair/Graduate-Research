      SUBROUTINE  DSNSGB(N, P,L, X,B,C,Y, CALCA,CALCB,
     1INC,IINC,   MXFCAL, ACC )
C
C ** SIMPLIED VERSION OF  DNSGB
C
C INPUT PARAMETERS
C N    NUMBER OF OBSERVATIONS
C P    NUMBER OF NONLINEAR UNKNOWNS
C L    NUMBER OF LINEAR UNKNOWNS
C X    APPROXIMATE SOLUTION NONLINEAR PARAMETERS
C B    FIRST ROW GIVES LOWER BOUNDS ON X. THE SECOND GIVES UPPER BOUNDS
C Y    OBSERVATIONS
C CALCA SUBROUTINE TO EVALUATE  THE A MATRIX
C CALCB SUBROUTINE TO COMPUTE THE DERIVATIVES OF A
C INC  INCIDENCE ARRAY IF INC(I,J)=1 THEN COLUMN I DEPENDS ON X(J)
C IINC ROW DIMENSIONOF INC MUST BE AT LEAST L+1
C MXFCAL MAXIMUM NUMBER OF PERMITTED FUNCTION EVALUATIONS
C ACC   ACCURACY IN X
C OUTPUT PARAMETERS
C X     SOLUTION
C C     LINEAR PARAMETERS
      INTEGER N, P, MXFCAL, L, IINC
      DOUBLE PRECISION X(P), ACC ,B(2,P)
      DOUBLE PRECISION C(L), Y(N)
      INTEGER INC(IINC,P)
      EXTERNAL CALCA
      EXTERNAL CALCB
C
C
C
C  ***  LOCAL VARIABLES  ***
C
      INTEGER IV, LIV, LV, V1
      DOUBLE PRECISION DSTAK(500)
      COMMON /CSTAK/ DSTAK
      INTEGER ISTAK(1000)
      EQUIVALENCE (DSTAK(1), ISTAK(1))
C
C  ***  BODY  ***
C
      CALL ENTER(0)
C/6S
C     IF (N.LT.1.OR.P.LT.0.OR.L.LT.0)
C    1CALL SETERR(34HDSNSGB- N.LT.1 OR P.LT.0 OR L.LT.0,34,1,2)
C     IF(IINC.LE.L)
C    1CALL SETERR(16HDSNSGB-IINC.LE.L,16,2,2)
C     IF (MXFCAL.LT.1)
C    1CALLSETERR(19HDSNSGB- MXFCAL.LT.1,19,4,2)
C     IF (ACC.LT.0.0D0)
C    1CALL SETERR(18HDSNSGB-ACC .LT.0.0,18,5,2)
C/7S
      IF (N.LT.1.OR.P.LT.0.OR.L.LT.0)
     1CALL SETERR('DSNSGB- N.LT.1 OR P.LT.0 OR L.LT.0',34,1,2)
      IF(IINC.LE.L)
     1CALL SETERR('DSNSGB-IINC.LE.L',16,2,2)
      IF (MXFCAL.LT.1)
     1CALLSETERR('DSNSGB- MXFCAL.LT.1',19,4,2)
      IF (ACC.LT.0.0D0)
     1CALL SETERR('DSNSGB-ACC .LT.0.0',18,5,2)
C/
      LP1=L+1
C  ***  CHECK INC, COUNT ITS NONZEROS
C
      L1 = 0
      M = 0
      DO 40 I = 1, P
         M0 = M
         IF (L .EQ. 0) GO TO 20
         DO 10 K = 1, L
            IF (INC(K,I) .LT. 0 .OR. INC(K,I) .GT. 1) GO TO 50
            IF (INC(K,I) .EQ. 1) M = M + 1
 10         CONTINUE
 20      IF (INC(LP1,I) .NE. 1) GO TO 30
            M = M + 1
            L1 = 1
 30      IF (M .EQ. M0 .OR. INC(LP1,I) .LT. 0
     1                 .OR. INC(LP1,I) .GT. 1) GO TO 50
 40      CONTINUE
      GO TO 60
C/6S
C50   CALL SETERR(33HDSNSGB- INCIDENCE ARRAY INCORRECT,33,3,2)
C/7S
 50   CALL SETERR('DSNSGB- INCIDENCE ARRAY INCORRECT',33,3,2)
C/
 60   CONTINUE
      LIV = 115+P+L+2*M+3*P
      LV = 105+P*(2*P+21)+N*(L+M+3)+L*(L+3)/2+N*P
      IV=ISTKGT(LIV,2)
      V1=ISTKGT(LV, 4)
      CALL DIVSET(1,ISTAK(IV),LIV,LV,DSTAK(V1))
      ISTAK(IV+20)=0
      ISTAK(IV+16)=MXFCAL
      ISTAK(IV+17)=MXFCAL
      ISTAK(IV+56) =0
      DSTAK(V1+32)=ACC
      DSTAK(V1+31)=ACC
      CALL  DN6SGB(N, P,L, X,B,C,Y,  CALCA,CALCB,INC,IINC,
     1  ISTAK(IV), LIV, LV,DSTAK(V1))
      J=ISTAK(IV)
      IF(J.LT.7) GO TO 70
C/6S
C     IF (J.EQ.82)CALL SETERR(26HDSNSGB-INCONSISTENT BOUNDS,26,6,1)
C     IF (J.EQ.7)CALL SETERR(27HDSNSGB-SINGULAR CONVERGENCE,27,7,1)
C     IF(J.EQ.8)CALL SETERR(24HDSNSGB-FALSE CONVERGENCE,24,8,1)
C     IF(J.EQ.9)CALL SETERR(32HDSNSGB-FUNCTION EVALUATION LIMIT,32,9,1)
C     IF (J.EQ.63)
C    1CALL SETERR(43HDSNSGB-A(X) CANNOT BE COMPUTED AT INITIAL X,43,10,
C    21)
C     IF(J.EQ.65)
C    1CALL SETERR(47HDSNSGB-JACOBIAN CANNOT BE COMPUTED AT INITIAL X,
C    2 47,11,1)
C/7S
      IF (J.EQ.82)CALL SETERR('DSNSGB-INCONSISTENT BOUNDS',26,6,1)
      IF (J.EQ.7)CALL SETERR('DSNSGB-SINGULAR CONVERGENCE',27,7,1)
      IF(J.EQ.8)CALL SETERR('DSNSGB-FALSE CONVERGENCE',24,8,1)
      IF(J.EQ.9)CALL SETERR('DSNSGB-FUNCTION EVALUATION LIMIT',32,9,1)
      IF (J.EQ.63)
     1CALL SETERR('DSNSGB-A(X) CANNOT BE COMPUTED AT INITIAL X',43,10,
     21)
      IF(J.EQ.65)
     1CALL SETERR('DSNSGB-JACOBIAN CANNOT BE COMPUTED AT INITIAL X',
     2 47,11,1)
C/
 70    CALL LEAVE
C
      RETURN
C  *** LAST LINE OF  DSNSGB FOLLOWS  ***
      END
