      REAL FUNCTION MKFL( E, M )
C
C     MKFL RETURNS (B**E) * M, WHERE B = I1MACH(10)
C
C     CHANGED JUNE 4, 1976
C
C     INPUT NEED NOT BE NORMALIZED
C
      REAL M
      REAL S2MACH
      INTEGER E, TE
C
      REAL TBASE
      INTEGER BASE, MINEXP, MAXEXP
C
      DATA TBASE / 0.0E0 /
      DATA BASE, MINEXP, MAXEXP / 3*0 /
C
      IF( BASE .NE. 0 ) GO TO 5
        BASE   = I1MACH(10)
        MINEXP = I1MACH(12)
        MAXEXP = I1MACH(13)
        TBASE  = BASE
C
  5   TE = E
      MKFL = M
C
      IF( ABS(M) .GE. 1.0E0 ) GO TO 10
C
      IF( TBASE*ABS(M) .GE. 1.0E0 ) GO TO 20
      IF( ABS(M) .EQ. 0.0E0 ) RETURN
C
C     COMES TO HERE IF ABS(M) TOO LARGE OR TOO SMALL.
C
 10   CALL UMKFL( M, TE, MKFL )
      TE = E + TE
C
C     NOW M AND TE ARE OK.
C
C/6S
C20   IF( TE .GT. MAXEXP ) CALL SETERR( 15HMKFL - OVERFLOW, 15, 1, 2 )
C/7S
 20   IF( TE .GT. MAXEXP ) CALL SETERR( 'MKFL - OVERFLOW', 15, 1, 2 )
C/
C
      IF( TE .LT. MINEXP )  MKFL = 0.0E0
C
      MKFL = S2MACH( MKFL, BASE, TE )
C
      RETURN
      END
