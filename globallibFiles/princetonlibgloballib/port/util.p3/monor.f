      LOGICAL FUNCTION MONOR(X, N, INC)
      INTEGER INC
      INTEGER N
      REAL X(INC, 1)
      INTEGER J, I
C   MONOR IS AN LOGICAL FUNCTION WHICH INSPECTS THE REAL
C   VECTOR, X, AND RETURNS THE VALUE, TRUE, IF THE ARRAY IS
C   MONOTONE AND THE VALUE, FALSE, OTHERWISE.  THE VALUE, FALSE, IS
C   RETURNED ONLY IF THE ARRAY CONTAINS ELMENTS WHICH CONTRADICT
C   THE PROPERTY.  IN PARTICULAR, VECTORS OF LENGTH 0, 1, OR 2
C   RETURN TRUE.
C   X   - THE VECTOR OF POINTS TO BE CHECKED.
C   N   - THE DIMENSION OF THE VECTOR X.
C         N .LT. 0    IS A FATAL ERROR.
C   INC - THE INCREMENT IN THE VECTOR.
C         INC .LT. 1  IS A FATAL ERROR.
C/6S
C     IF (N .LT. 0) CALL SETERR(25HMONOR - INVALID DIMENSION, 25, 1, 2)
C     IF (INC .LT. 1) CALL SETERR(25HMONOR - INVALID INCREMENT, 25, 2, 2
C    1   )
C/7S
      IF (N .LT. 0) CALL SETERR('MONOR - INVALID DIMENSION', 25, 1, 2)
      IF (INC .LT. 1) CALL SETERR('MONOR - INVALID INCREMENT', 25, 2, 2
     1   )
C/
      MONOR = .TRUE.
      IF (N .LT. 3) RETURN
C MONOTONE BY DEFAULT.
C PASS OVER INITIAL EQUALITIES.
      DO  1 J = 2, N
         IF (X(1, J-1) .NE. X(1, J)) GOTO  2
   1     CONTINUE
      RETURN
C CHECK FOR MONOTONE INCREASING.
   2  DO  3 I = J, N
         IF (X(1, I-1) .GT. X(1, I)) GOTO  4
   3     CONTINUE
      RETURN
C CHECK FOR MONOTONE DECREASING.
   4  DO  5 I = J, N
         IF (X(1, I-1) .LT. X(1, I)) GOTO  6
   5     CONTINUE
      RETURN
   6  MONOR = .FALSE.
C NOT MONOTONE.
      RETURN
      END
