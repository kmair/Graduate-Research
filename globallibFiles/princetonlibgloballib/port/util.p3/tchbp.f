      REAL FUNCTION TCHBP(N, ALPHA, X, X0, X1)
      INTEGER N
      REAL ALPHA(1), X, X0, X1
      INTEGER K
      REAL Z, TWOZ, BETAK, BETAK1, BETAK2
C  TCHBP IS A REAL FORTRAN FUNCTION WHICH RETURNS THE VALUE
C  OF A POLYNOMIAL, P(X), OF DEGREE N GIVEN BY ITS TCHEBYCHEFF
C  EXPANSION ON THE INTERVAL (X0,X1).
C  N      - THE DEGREE OF P(X).
C  ALPHA  - THE N+1 COEFFICIENTS IN THE EXPANSION OF P(X).
C  X      - THE ARGUMENT AT WHICH P(X) IS TO BE EVALUATED.
C  X0,X1  - THE LEFT AND RIGHT HAND END POINTS OF THE INTERVAL
C           FOR WHICH THE EXPANSION IS CONSTRUCTED.
C  ERROR STATES-
C       1 - INVALID DEGREE.
C       2 - INVALID INTERVAL.
C/6S
C     IF (N .LT. 0) CALL SETERR(22HTCHBP - INVALID DEGREE, 22, 1, 2)
C/7S
      IF (N .LT. 0) CALL SETERR('TCHBP - INVALID DEGREE', 22, 1, 2)
C/
      IF (N .NE. 0) GOTO 1
         TCHBP = ALPHA(1)
         GOTO  5
   1     Z = X1-X0
C  SCALE X TO THE INTERVAL (X0,X1).
C/6S
C        IF (Z .LE. 0.0) CALL SETERR(24HTCHBP - INVALID INTERVAL, 24, 2,
C    1      2)
C/7S
         IF (Z .LE. 0.0) CALL SETERR('TCHBP - INVALID INTERVAL', 24, 2,
     1      2)
C/
         Z = ((X-X0)+(X-X1))/Z
         TWOZ = 2.0*Z
         BETAK2 = 0.0
         BETAK1 = ALPHA(N+1)
         K = N
            GOTO  3
   2        K = K-1
   3        IF (K .LT. 2)GOTO  4
            BETAK = TWOZ*BETAK1-BETAK2+ALPHA(K)
            BETAK2 = BETAK1
            BETAK1 = BETAK
            GOTO  2
   4     TCHBP = Z*BETAK1-BETAK2+ALPHA(1)
   5  RETURN
      END
