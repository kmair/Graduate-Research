      LOGICAL FUNCTION D3PLND(K,T,N,A,ID,NID,ILO,IDIM,ADIFF)
C
C  TO SET
C
C  ADIFF(I,J) = A(ID(J))(ILO+I+ID(J)-K)
C
C  FOR I=1,...,IDIM-ID(J) AND J=1,...,NID.
C
C  THE FOLLOWING ASSUMPTIONS ARE MADE
C
C  0.LE.ID(1).LT. ... .LT.ID(NID).LT.K
C  1.LE.ILO.LT.N
C  K.LE.IDIM.LT.N+K-ILO
C  A(N-K) LONG
C
C  D3PLND=.TRUE. IS RETURNED IF SUCCESSFUL.
C  D3PLND=.FALSE. IS RETURNED IF THE MESH T IS NOT MONOTONE INCREASING.
C
      DOUBLE PRECISION T(N),A(1),ADIFF(IDIM,NID)
C     DOUBLE PRECISION A(N-K)
      INTEGER ID(NID)
C
      DOUBLE PRECISION FKMD,DIFF
C
      D3PLND=.TRUE.
C
      NMK=N-K
      IDX=ILO-K
C
C ... GET A(0)(I)
C
      DO 20 I=1,IDIM
         IDX=IDX+1
         IF (IDX.LT.1.OR.IDX.GT.NMK) GO TO 10
         ADIFF(I,1)=A(IDX)
         GO TO 20
 10      ADIFF(I,1)=0.0D0
 20      CONTINUE
C
C ... GET ADIFF(I,J)
C
      IDERIV=0
      J=1
 30      JM=J
         IF (IDERIV.EQ.ID(J)) J=J+1
         IF (J.GT.NID) GO TO 60
         IDERIV=IDERIV+1
         IDIMD=IDIM-IDERIV
         KMD=K-IDERIV
         FKMD=FLOAT(KMD)
         IPDMK=ILO-KMD
         DO 40 I=1,IDIMD
            IDX1=MIN0(ILO+I,N)
            IDX2=MAX0(IPDMK+I,1)
            DIFF=T(IDX1)-T(IDX2)
C
            IF (DIFF.LT.0.0D0) GO TO 50
C
            IF (DIFF.GT.0.0D0)
     1          ADIFF(I,J)=FKMD*(ADIFF(I+1,JM)-ADIFF(I,JM))/DIFF
 40         CONTINUE
         GO TO 30
C
 50   D3PLND=.FALSE.
C
 60   RETURN
C
      END
