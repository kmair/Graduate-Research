      SUBROUTINE DBSPD1(G,N,M,B)
C
C  TO SOLVE L*X=B, WHERE L IS
C  THE CHOLESKY FACTOR OF A BANDED, SYMMETRIC, POSITIVE-DEFINITE
C  MATRIX AS DESCRIBED IN DBSPDD.
C
C  INPUT
C
C    G - G(I,J)=L(I,I+J-M) STORES THE LOWER TRIANGULAR PORTION OF L
C        FOR I=1,...,N AND J=1,...,M.
C    N - THE ORDER OF THE MATRIX L.
C    M - THE NUMBER OF ENTRIES OF L ON OR BELOW THE DIAGONAL.
C    B - THE RIGHT-HAND SIDE.
C
C  OUTPUT
C
C    B - THE SOLUTION X.
C
C  SCRATCH SPACE ALLOCATED - NONE.
C
C  ERROR STATES
C
C    1 - N.LT.1
C    2 - M.LT.1
C    3 - SINGULAR L.
C
      DOUBLE PRECISION G(N,M),B(N)
C
      DOUBLE PRECISION T
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (N.LT.1) CALL SETERR(15HDBSPD1 - N.LT.1,15,1,2)
C     IF (M.LT.1) CALL SETERR(15HDBSPD1 - M.LT.1,15,2,2)
C/7S
      IF (N.LT.1) CALL SETERR('DBSPD1 - N.LT.1',15,1,2)
      IF (M.LT.1) CALL SETERR('DBSPD1 - M.LT.1',15,2,2)
C/
C
      MM1=M-1
C
C ... FORWARD ELIMINATION.
C
      IF (G(1,M).LE.0.0D0) GO TO 30
C
      B(1)=B(1)/G(1,M)
      IF (N.EQ.1) GO TO 40
      DO 20 I=2,N
C
         IF (G(I,M).LE.0.0D0) GO TO 30
C
         T=0.0D0
         IF (M.EQ.1) GO TO 20
         JLO=MAX0(I-MM1,1)
         JTOP=I-1
         JDX=JLO+M-I
         DO 10 J=JLO,JTOP
            T=T+G(I,JDX)*B(J)
 10         JDX=JDX+1
 20      B(I)=(B(I)-T)/G(I,M)
      GO TO 40
C
C ... HERE FOR A SINGULAR CHOLESKY DECOMPOSITION.
C
C/6S
C30   CALL SETERR(40HDBSPD1 - SINGULAR CHOLESKY DECOMPOSITION,40,3,2)
C/7S
 30   CALL SETERR('DBSPD1 - SINGULAR CHOLESKY DECOMPOSITION',40,3,2)
C/
C
 40   RETURN
C
      END
