      INTEGER FUNCTION IDPUM1(XB,NXB,NA,ML,MI,MR,NX)
C
C  TO ALLOCATE AND SET UP THE GRID X WHICH INCLUDES THE POINTS
C
C            XB(I)+(J-1)*((XB(I+1)-XB(I))/(NA(I)-1))
C
C  FOR I=1,...,NXB-1 AND J=1,...,NA(I).
C
C  INPUT -
C
C    XB  - THE BASIC POINTS FOR DEFINING THE GRID X.
C          XB MUST BE EITHER MONOTONE INCREASING OR MONOTONE DECREASING.
C          ANY MULTIPLICITIES IN XB ARE IGNORED.
C    NXB - THE NUMBER OF POINTS IN XB.
C    NA  - NA(I) GIVES THE NUMBER OF POINTS TO BE USED IN THE GRID
C          X FROM XB(I) TO XB(I+1), FOR I=1,...,NXB-1.
C    ML  - THE MULTIPLICITY OF XB(1) IN X.
C    MI  - THE MULTIPLICITY OF ALL X POINTS STRICTLY
C          BETWEEN XB(1) AND XB(NXB).
C    MR  - THE MULTIPLICITY OF XB(NXB) IN X.
C
C  OUTPUT -
C
C    X  - X(I)=WS(I+IDPUM1-1) FOR I=1,...,NX.
C    NX - THE NUMBER OF MESH POINTS IN X.
C
C           NX = ML +
C                (SUM(I=1,...,NXB-1,XB(I).NE.XB(I+1))(NA(I)-1) -1)*MI +
C                MR.
C
C  SCRATCH SPACE ALLOCATED - NX DOUBLE PRECISION WORDS ARE LEFT
C                            ON THE STACK.
C
C  ERROR STATES -
C
C    1 - NXB.LT.2.
C    2 - ONE OF (ML,MI,MR) IS NEGATIVE.
C    3 - XB IS NOT MONOTONE.
C    4 - NA(I).LT.2.
C
      DOUBLE PRECISION XB(NXB)
      INTEGER NA(1)
C     INTEGER NA(NXB-1)
C
      DOUBLE PRECISION DIR
C
      COMMON /CSTAK/DS
      DOUBLE PRECISION DS(500)
      DOUBLE PRECISION WS(1)
      EQUIVALENCE (DS(1),WS(1))
C
C/6S
C     IF (NXB.LT.2) CALL SETERR(17HIDPUM1 - NXB.LT.2,17,1,2)
C     IF (ML.LT.0.OR.MI.LT.0.OR.MR.LT.0) CALL SETERR
C    1  (38HIDPUM1 - ONE OF (ML,MI,MR) IS NEGATIVE,38,2,2)
C/7S
      IF (NXB.LT.2) CALL SETERR('IDPUM1 - NXB.LT.2',17,1,2)
      IF (ML.LT.0.OR.MI.LT.0.OR.MR.LT.0) CALL SETERR
     1  ('IDPUM1 - ONE OF (ML,MI,MR) IS NEGATIVE',38,2,2)
C/
C
      DIR=XB(NXB)-XB(1)
C/6S
C     IF (DIR.EQ.0.0D0) CALL SETERR
C    1  (27HIDPUM1 - XB IS NOT MONOTONE,27,3,2)
C/7S
      IF (DIR.EQ.0.0D0) CALL SETERR
     1  ('IDPUM1 - XB IS NOT MONOTONE',27,3,2)
C/
C
      NXBM1=NXB-1
      NXI=ML+MR-MI
      DO 10 I=1,NXBM1
        IF (XB(I).EQ.XB(I+1)) GO TO 10
C/6S
C       IF ((DIR/DABS(DIR))*(XB(I+1)-XB(I)).LT.0.0D0) CALL SETERR
C    1    (27HIDPUM1 - XB IS NOT MONOTONE,27,3,2)
C       IF (NA(I).LT.2) CALL SETERR(19HIDPUM1 - NA(I).LT.2,19,4,2)
C/7S
        IF ((DIR/DABS(DIR))*(XB(I+1)-XB(I)).LT.0.0D0) CALL SETERR
     1    ('IDPUM1 - XB IS NOT MONOTONE',27,3,2)
        IF (NA(I).LT.2) CALL SETERR('IDPUM1 - NA(I).LT.2',19,4,2)
C/
        NXI=NXI+(NA(I)-1)*MI
 10     CONTINUE
C
C ... ALLOCATE SPACE FOR THE MESH X.
C
      IDPUM1=ISTKGT(NXI,4)
C
      CALL DPUM1(XB,NXB,NA,ML,MI,MR,WS(IDPUM1),NX)
C
      RETURN
C
      END
