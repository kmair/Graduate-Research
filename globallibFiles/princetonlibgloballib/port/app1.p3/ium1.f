      INTEGER FUNCTION IUM1(A,B,NAB,MA,MI,MB,NX)
C
C  TO SET UP THE MESH X(1),...,X(NX) ON (A,B) WHICH INCLUDES THE
C  POINTS A + (I-1)*((B-A)/(NAB-1)), I=1,...,NAB WITH MULTIPLICITIES
C  MA FOR I=1, MI FOR 1.LT.I.LT.NAB AND MB FOR I=NAB.
C
C  INPUT -
C
C    A   - LEFT HAND END POINT.
C    B   - RIGHT HAND END POINT.
C    NAB - NUMBER OF POINTS TO BE USED ON (A,B).
C    MA  - THE MULTIPLICITY OF A IN X.
C    MI  - THE MULTIPLICITY OF ALL INTERIOR POINTS OF X.
C    MB  - THE MULTIPLICITY OF B IN X.
C
C  OUTPUT -
C
C    NX    - THE NUMBER OF MESH POINTS IN X,
C
C               NX = MA + MI*(NAB-2) + MB.
C
C    IUM1 - THE POINTER TO THE MESH OF LENGTH NX IN THE STACK.
C
C  SCRATCH SPACE ALLOCATED - NX REAL LOCATIONS
C                            ARE LEFT ON THE STACK.
C
C  ERROR STATES -
C
C    1 - A=B.
C    2 - NAB.LT.2.
C    3 - ONE OF (MA,MI,MB) IS NEGATIVE.
C
      REAL A,B
C
      COMMON /CSTAK/DS
      DOUBLE PRECISION DS(500)
      REAL WS(1)
      EQUIVALENCE (DS(1),WS(1))
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (A.EQ.B) CALL SETERR(11H IUM1 - A=B,11,1,2)
C     IF (NAB.LT.2) CALL SETERR(16H IUM1 - NAB.LT.2,16,2,2)
C     IF (MA.LT.0.OR.MI.LT.0.OR.MB.LT.0) CALL SETERR
C    1   (37H IUM1 - ONE OF (MA,MI,MB) IS NEGATIVE,37,3,2)
C/7S
      IF (A.EQ.B) CALL SETERR(' IUM1 - A=B',11,1,2)
      IF (NAB.LT.2) CALL SETERR(' IUM1 - NAB.LT.2',16,2,2)
      IF (MA.LT.0.OR.MI.LT.0.OR.MB.LT.0) CALL SETERR
     1   (' IUM1 - ONE OF (MA,MI,MB) IS NEGATIVE',37,3,2)
C/
C
C ... ALLOCATE THE MESH.
C
      IUM1=ISTKGT(MA+MI*(NAB-2)+MB,3)
C
      CALL UM1(A,B,NAB,MA,MI,MB,WS(IUM1),NX)
C
      RETURN
C
      END
