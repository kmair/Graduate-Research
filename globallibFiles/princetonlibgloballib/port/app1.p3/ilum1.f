      INTEGER FUNCTION ILUM1(XB,NXB,N,ML,MI,MR,NX)
C
C  TO ALLOCATE AND SET UP THE GRID X WHICH INCLUDES THE POINTS
C
C            XB(I)+(J-1)*((XB(I+1)-XB(I))/(N-1))
C
C  FOR I=1,...,NXB-1 AND J=1,...,N.
C
C  INPUT -
C
C    XB  - THE BASIC POINTS FOR DEFINING THE GRID X.
C          XB MUST BE EITHER MONOTONE INCREASING OR MONOTONE DECREASING.
C          ANY MULTIPLICITIES IN XB ARE IGNORED.
C    NXB - THE NUMBER OF POINTS IN XB.
C    N   - THE NUMBER OF POINTS TO BE USED IN THE GRID
C          X FROM XB(I) TO XB(I+1), FOR I=1,...,NXB-1.
C    ML  - THE MULTIPLICITY OF XB(1) IN X.
C    MI  - THE MULTIPLICITY OF ALL X POINTS STRICTLY
C          BETWEEN XB(1) AND XB(NXB).
C    MR  - THE MULTIPLICITY OF XB(NXB) IN X.
C
C  OUTPUT -
C
C    X  - X(I)=WS(I+ILUM1-1) FOR I=1,...,NX.
C    NX - THE NUMBER OF MESH POINTS IN X.
C
C           NX = ML +
C                (SUM(I=1,...,NXB-1,XB(I).NE.XB(I+1))(N-1) -1)*MI +
C                MR.
C
C  SCRATCH SPACE ALLOCATED - NX REAL WORDS ARE LEFT
C                            ON THE STACK.
C
C  ERROR STATES -
C
C    1 - NXB.LT.2.
C    2 - ONE OF (ML,MI,MR) IS NEGATIVE.
C    3 - XB IS NOT MONOTONE.
C    4 - N.LT.2.
C
      REAL XB(NXB)
      INTEGER N
C
      REAL DIR
C
      COMMON /CSTAK/DS
      DOUBLE PRECISION DS(500)
      REAL WS(1)
      EQUIVALENCE (DS(1),WS(1))
C
C/6S
C     IF (NXB.LT.2) CALL SETERR(17H ILUM1 - NXB.LT.2,17,1,2)
C     IF (ML.LT.0.OR.MI.LT.0.OR.MR.LT.0) CALL SETERR
C    1  (38H ILUM1 - ONE OF (ML,MI,MR) IS NEGATIVE,38,2,2)
C/7S
      IF (NXB.LT.2) CALL SETERR(' ILUM1 - NXB.LT.2',17,1,2)
      IF (ML.LT.0.OR.MI.LT.0.OR.MR.LT.0) CALL SETERR
     1  (' ILUM1 - ONE OF (ML,MI,MR) IS NEGATIVE',38,2,2)
C/
C
      DIR=XB(NXB)-XB(1)
C/6S
C     IF (DIR.EQ.0.0E0) CALL SETERR
C    1  (27H ILUM1 - XB IS NOT MONOTONE,27,3,2)
C/7S
      IF (DIR.EQ.0.0E0) CALL SETERR
     1  (' ILUM1 - XB IS NOT MONOTONE',27,3,2)
C/
C
      NXBM1=NXB-1
      NXI=ML+MR-MI
      DO 10 I=1,NXBM1
        IF (XB(I).EQ.XB(I+1)) GO TO 10
C/6S
C       IF ((DIR/ABS(DIR))*(XB(I+1)-XB(I)).LT.0.0E0) CALL SETERR
C    1    (27H ILUM1 - XB IS NOT MONOTONE,27,3,2)
C       IF (N.LT.2) CALL SETERR(15H ILUM1 - N.LT.2,15,4,2)
C/7S
        IF ((DIR/ABS(DIR))*(XB(I+1)-XB(I)).LT.0.0E0) CALL SETERR
     1    (' ILUM1 - XB IS NOT MONOTONE',27,3,2)
        IF (N.LT.2) CALL SETERR(' ILUM1 - N.LT.2',15,4,2)
C/
        NXI=NXI+(N-1)*MI
 10     CONTINUE
C
C ... ALLOCATE SPACE FOR THE MESH X.
C
      ILUM1=ISTKGT(NXI,3)
C
      CALL LUM1(XB,NXB,N,ML,MI,MR,WS(ILUM1),NX)
C
      RETURN
C
      END
