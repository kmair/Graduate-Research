      SUBROUTINE DBSPD2(G,N,M,B)
C
C  TO SOLVE (L-TRANSPOSE)*X=B, WHERE L IS
C  THE CHOLESKY FACTOR OF A BANDED, SYMMETRIC, POSITIVE-DEFINITE
C  MATRIX AS DESCRIBED IN DBSPDD.
C
C  INPUT
C
C    G - G(I,J)=L(I,I+J-M) STORES THE LOWER TRIANGULAR PORTION OF L
C        FOR I=1,...,N AND J=1,...,M.
C    N - THE ORDER OF THE MATRIX L.
C    M - THE NUMBER OF ENTRIES OF L ON OR BELOW THE DIAGONAL.
C    B - THE RIGHT-HAND SIDE.
C
C  OUTPUT
C
C    B - THE SOLUTION X.
C
C  SCRATCH SPACE ALLOCATED - NONE.
C
C  ERROR STATES
C
C    1 - N.LT.1
C    2 - M.LT.1
C    3 - SINGULAR L.
C
      DOUBLE PRECISION G(N,M),B(N)
C
      DOUBLE PRECISION T
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (N.LT.1) CALL SETERR(15HDBSPD2 - N.LT.1,15,1,2)
C     IF (M.LT.1) CALL SETERR(15HDBSPD2 - M.LT.1,15,2,2)
C/7S
      IF (N.LT.1) CALL SETERR('DBSPD2 - N.LT.1',15,1,2)
      IF (M.LT.1) CALL SETERR('DBSPD2 - M.LT.1',15,2,2)
C/
C
      MM1=M-1
C
C ... BACK SUBSTITUTION.
C
      IF (G(N,M).LE.0.0D0) GO TO 40
C
      B(N)=B(N)/G(N,M)
      IF (N.EQ.1) GO TO 50
      I=N
C
C ... DECREASING LOOP ON I.
C
 10      I=I-1
         IF (G(I,M).LE.0.0D0) GO TO 40
         T=0.0D0
         IF (M.EQ.1) GO TO 30
         JLO=I+1
         JTOP=MIN0(I+MM1,N)
         JDX=MM1
         DO 20 J=JLO,JTOP
            T=T+G(J,JDX)*B(J)
 20         JDX=JDX-1
 30      B(I)=(B(I)-T)/G(I,M)
         IF (I.GT.1) GO TO 10
      GO TO 50
C
C ... HERE FOR A SINGULAR CHOLESKY DECOMPOSITION.
C
C/6S
C40   CALL SETERR(40HDBSPD2 - SINGULAR CHOLESKY DECOMPOSITION,40,3,2)
C/7S
 40   CALL SETERR('DBSPD2 - SINGULAR CHOLESKY DECOMPOSITION',40,3,2)
C/
C
 50   RETURN
C
      END
