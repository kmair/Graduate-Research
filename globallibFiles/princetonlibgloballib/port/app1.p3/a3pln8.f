      LOGICAL FUNCTION A3PLN8(K,T,N,X,NX,ILEFT,ID,NID,BX,IFA)
C
C  TO OBTAIN ALL NON-ZERO BASIS SPLINES AND THEIR DERIVATIVES,
C
C           BX(IX,I,J) = B(ILEFT+I-K) SUP (ID(J)) (X(IX))
C
C  FOR IX=1,...,NX, I=1,...,K AND J=1,...,NID.
C
C  INPUT
C
C    K     - THE ORDER OF THE B-SPLINES TO BE USED.
C            2.LE.K IS ASSUMED.
C    T     - THE B-SPLINE MESH.
C    N     - THE NUMBER OF POINTS IN THE MESH T.
C    X     - POINTS OF EVALUATION FOR THE B-SPLINE.
C    NX    - THE NUMBER OF POINTS IN X.
C    ILEFT - ALL POINTS X(IX) ARE ASSUMED TO LIE IN THE SAME INTERVAL
C            (T(ILEFT),T(ILEFT+1)).
C    ID    - DERIVATIVES OF THE B-SPLINE DESIRED.
C            0.LE.ID(1).LT.ID(2).LT. ... .LT.ID(NID).LT.K IS ASSUMED.
C    NID   - THE NUMBER OF DERIVATIVES DESIRED, THE LENGTH OF ID.
C    IFA   - POINTER TO
C
C                 MAX(3*K,K*(MIN((K+1)/2,ID(NID)+1)+1))
C
C            WORKING STACK SCRATCH LOCATIONS.
C
C  OUTPUT
C
C    BX     - THE BASIS SPLINES AND THEIR DERIVATIVES.
C    A3PLN8 - .TRUE. IF SUCCESSFUL.
C             .FALSE. IF T IS NOT MONOTONE INCREASING.
C
C  SCRATCH SPACE ALLOCATED - NONE.
C
C  ERROR STATES - NONE.
C
      REAL T(N),X(NX),BX(NX,K,NID)
      INTEGER ID(NID)
C
      LOGICAL A3PLNN,A3PLNB
C
      COMMON /CSTAK/DS
      DOUBLE PRECISION DS(500)
      REAL WS(1)
      EQUIVALENCE (DS(1),WS(1))
C
      A3PLN8=.TRUE.
C
      IDM=IFA+K
      IDP=IDM+K
C
C ... LOAD BX(IX,I,J) WITH N(ILEFT+I+ID(J)-K,K-ID(J))(X(IX)).
C
      DO 30 IX=1,NX
C
         JJ01=0
         J=NID
 10         KMD=K-ID(J)
C
            IF (.NOT.A3PLNN(T,N,X(IX),ILEFT,KMD,WS(IFA),WS(IDM),WS(IDP),
     1                      JJ01)) GO TO 40
C
            IDX=IFA
            DO 20 I=1,KMD
               BX(IX,I,J)=WS(IDX)
 20            IDX=IDX+1
            J=J-1
            IF (J.GE.1) GO TO 10
 30      CONTINUE
C
      IF (ID(NID).EQ.0) GO TO 50
C
C ... NOW GET THE HIGHER ORDER DERIVATIVES.
C
      IADIFF=IFA+K
C
      IF (.NOT.A3PLNB(T,N,K,X,NX,ILEFT,ID,NID,BX,WS(IADIFF),WS(IFA)))
     1  GO TO 40
C
      GO TO 50
C
 40   A3PLN8=.FALSE.
C
 50   RETURN
C
      END
