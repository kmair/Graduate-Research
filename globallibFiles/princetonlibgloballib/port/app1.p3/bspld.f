      SUBROUTINE BSPLD(K,T,N,
     1                  X,NX,ILEFT,
     2                  MD,
     3                  BX)
C
C  TO OBTAIN ALL NON-ZERO BASIS SPLINES AND THEIR DERIVATIVES,
C
C           BX(IX,I,J) = B(ILEFT+I-K) SUP (J-1) (X(IX)).
C
C  FOR IX=1,...,NX, I=1,...,K AND J=1,...,MD.
C
C  INPUT
C
C    K      - THE ORDER OF THE B-SPLINES TO BE USED.
C             2.LE.K IS ASSUMED.
C    T      - THE B-SPLINE MESH.
C    N      - THE NUMBER OF POINTS IN THE MESH T.
C    X      - POINTS OF EVALUATION FOR THE B-SPLINE.
C    NX     - THE NUMBER OF POINTS IN X.
C    ILEFT  - ALL POINTS X(IX) ARE ASSUMED TO LIE IN THE SAME INTERVAL
C             (T(ILEFT),T(ILEFT+1)).
C    MD     - DERIVATIVES 0 , ... , MD-1 ARE DESIRED.
C
C  OUTPUT
C
C    BX - THE BASIS SPLINES AND THEIR DERIVATIVES.
C
C  SCRATCH SPACE ALLOCATED -
C
C        MAX(3*K,K*(MIN((K+1)/2,MD)+1))
C
C  REAL WORDS + MD INTEGER WORDS.
C
C  ERROR STATES -
C
C     1 - K.LT.2.
C     2 - N.LE.K.
C     3 - NX.LT.1.
C     4 - ILEFT.LT.1.
C     5 - ILEFT.GE.N.
C     6 - T(ILEFT)=T(ILEFT+1).
C     7 - MD.LT.1.
C     8 - MD.GT.K.
C     9 - ALL X ARE NOT IN THE SAME ILEFT INTERVAL.
C    10 - T IS NOT MONOTONE INCREASING.
C
      REAL T(N),X(NX),BX(NX,K,MD)
C
      LOGICAL A3PLN8
C
      COMMON /CSTAK/DS
      DOUBLE PRECISION DS(500)
      REAL WS(1)
      INTEGER IS(1000)
      EQUIVALENCE (DS(1),WS(1),IS(1))
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (K.LT.2) CALL SETERR(15H BSPLD - K.LT.2,15,1,2)
C     IF (N.LE.K) CALL SETERR(15H BSPLD - N.LE.K,15,2,2)
C     IF (NX.LT.1) CALL SETERR(16H BSPLD - NX.LT.1,16,3,2)
C     IF (ILEFT.LT.1) CALL SETERR(19H BSPLD - ILEFT.LT.1,19,4,2)
C     IF (ILEFT.GE.N) CALL SETERR(19H BSPLD - ILEFT.GE.N,19,5,2)
C     IF (T(ILEFT).EQ.T(ILEFT+1)) CALL SETERR
C    1  (28H BSPLD - T(ILEFT)=T(ILEFT+1),28,6,2)
C     IF (MD.LT.1)
C    1   CALL SETERR(16H BSPLD - MD.LT.1,16,7,2)
C     IF (MD.GT.K)
C    1   CALL SETERR(16H BSPLD - MD.GT.K,16,8,2)
C/7S
      IF (K.LT.2) CALL SETERR(' BSPLD - K.LT.2',15,1,2)
      IF (N.LE.K) CALL SETERR(' BSPLD - N.LE.K',15,2,2)
      IF (NX.LT.1) CALL SETERR(' BSPLD - NX.LT.1',16,3,2)
      IF (ILEFT.LT.1) CALL SETERR(' BSPLD - ILEFT.LT.1',19,4,2)
      IF (ILEFT.GE.N) CALL SETERR(' BSPLD - ILEFT.GE.N',19,5,2)
      IF (T(ILEFT).EQ.T(ILEFT+1)) CALL SETERR
     1  (' BSPLD - T(ILEFT)=T(ILEFT+1)',28,6,2)
      IF (MD.LT.1)
     1   CALL SETERR(' BSPLD - MD.LT.1',16,7,2)
      IF (MD.GT.K)
     1   CALL SETERR(' BSPLD - MD.GT.K',16,8,2)
C/
C
C ... ALLOCATE SCRATCH SPACE.
C
      IFA=ISTKGT(MAX0(3*K,K*(MIN0((K+1)/2,MD)+1)),3)
C
      IID=ISTKGT(MD,2)
      DO 10 I=1,MD
        IDX=IID+I-1
 10     IS(IDX)=I-1
C
      DO 20 IX=1,NX
C/6S
C       IF (T(ILEFT).GT.X(IX).OR.X(IX).GT.T(ILEFT+1)) CALL SETERR
C    1    (49H BSPLD - ALL X ARE NOT IN THE SAME ILEFT INTERVAL,49,9,2)
C       IF (X(IX).EQ.T(ILEFT+1).AND.T(ILEFT+1).LT.T(N)) CALL SETERR
C    1    (49H BSPLD - ALL X ARE NOT IN THE SAME ILEFT INTERVAL,49,9,2)
C/7S
        IF (T(ILEFT).GT.X(IX).OR.X(IX).GT.T(ILEFT+1)) CALL SETERR
     1    (' BSPLD - ALL X ARE NOT IN THE SAME ILEFT INTERVAL',49,9,2)
        IF (X(IX).EQ.T(ILEFT+1).AND.T(ILEFT+1).LT.T(N)) CALL SETERR
     1    (' BSPLD - ALL X ARE NOT IN THE SAME ILEFT INTERVAL',49,9,2)
C/
 20     CONTINUE
C
C/6S
C     IF (.NOT.A3PLN8(K,T,N,X,NX,ILEFT,IS(IID),MD,BX,IFA))
C    1  CALL SETERR(37H BSPLD - T IS NOT MONOTONE INCREASING,37,10,2)
C/7S
      IF (.NOT.A3PLN8(K,T,N,X,NX,ILEFT,IS(IID),MD,BX,IFA))
     1  CALL SETERR(' BSPLD - T IS NOT MONOTONE INCREASING',37,10,2)
C/
C
      CALL ISTKRL(2)
C
      RETURN
C
      END
