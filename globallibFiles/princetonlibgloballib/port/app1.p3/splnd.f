      SUBROUTINE SPLND(K,T,N,A,
     1                  X,NX,
     2                  MD,
     3                  FX)
C
C  TO EVALUATE THE B-SPLINE AND ITS DERIVATIVES
C
C            FX(IX,J)=F(J-1)(X(IX))
C
C  FOR IX=1,...,NX AND J=1,...,MD, WHERE
C
C        F(X)=SUM(I=1,...,N-K)(A(I)*B(I)(X)).
C
C  IF MANY SMALL NX CALLS TO SPLND ARE ANTICIPATED, IT WOULD BE CHEAPER
C  TO USE SPLN2, SINCE SPLN2 SAVES INTERMEDIATE RESULTS
C  FROM ONE CALL TO THE NEXT.
C
C  INPUT
C
C    K      - THE ORDER OF THE B-SPLINES TO BE USED.
C             2.LE.K IS ASSUMED.
C    T      - THE B-SPLINE MESH.
C    N      - THE NUMBER OF POINTS IN THE MESH T.
C    A      - THE B-SPLINE COEFFICIENTS, N-K OF THEM.
C    X      - POINTS OF EVALUATION FOR THE B-SPLINE.
C             X MONOTONE INCREASING WOULD HELP, BUT IT IS NOT NECESSARY.
C    NX     - THE NUMBER OF POINTS IN X.
C    MD     - DERIVATIVES 0 , ... , MD-1 ARE DESIRED.
C
C  OUTPUT
C
C    FX - THE VALUES OF F AND ITS DERIVATIVES.
C
C  SCRATCH SPACE ALLOCATED - 3*K*(MD+1) REAL WORDS +
C                            MD INTEGER WORDS.
C
C  ERROR STATES -
C
C    1 - K.LT.2.
C    2 - N.LE.K.
C    3 - NX.LT.1.
C    4 - MD.LT.1.
C    5 - MD.GT.K.
C    6 - T IS NOT MONOTONE INCREASING.
C
      REAL T(N),A(1),X(NX),FX(NX,MD)
C     REAL A(N-K).
C
      LOGICAL A3PLNE
C
      COMMON /CSTAK/DS
      DOUBLE PRECISION DS(500)
      REAL WS(1)
      INTEGER IS(1000)
      EQUIVALENCE (DS(1),WS(1),IS(1))
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (K.LT.2)
C    1   CALL SETERR(15H SPLND - K.LT.2,15,1,2)
C     IF (N.LE.K)
C    1   CALL SETERR(15H SPLND - N.LE.K,15,2,2)
C     IF (NX.LT.1)
C    1   CALL SETERR(16H SPLND - NX.LT.1,16,3,2)
C     IF (MD.LT.1)
C    1   CALL SETERR(16H SPLND - MD.LT.1,16,4,2)
C     IF (MD.GT.K)
C    1   CALL SETERR(16H SPLND - MD.GT.K,16,5,2)
C/7S
      IF (K.LT.2)
     1   CALL SETERR(' SPLND - K.LT.2',15,1,2)
      IF (N.LE.K)
     1   CALL SETERR(' SPLND - N.LE.K',15,2,2)
      IF (NX.LT.1)
     1   CALL SETERR(' SPLND - NX.LT.1',16,3,2)
      IF (MD.LT.1)
     1   CALL SETERR(' SPLND - MD.LT.1',16,4,2)
      IF (MD.GT.K)
     1   CALL SETERR(' SPLND - MD.GT.K',16,5,2)
C/
C
C ... ALLOCATE SCRATCH SPACE.
C
      IADIFF=ISTKGT(3*K*(MD+1),3)
      IID=ISTKGT(MD,2)
C
      DO 10 I=1,MD
        IDX=IID+I-1
 10     IS(IDX)=I-1
C
      ILEFT=(N+1)/2
      ILO=0
C/6S
C     IF (.NOT.A3PLNE(K,T,N,A,X,NX,IS(IID),MD,FX,
C    1                3*K,WS(IADIFF),ILO,ILEFT,IADIFF+3*K*MD))
C    2  CALL SETERR(37H SPLND - T IS NOT MONOTONE INCREASING,37,6,2)
C/7S
      IF (.NOT.A3PLNE(K,T,N,A,X,NX,IS(IID),MD,FX,
     1                3*K,WS(IADIFF),ILO,ILEFT,IADIFF+3*K*MD))
     2  CALL SETERR(' SPLND - T IS NOT MONOTONE INCREASING',37,6,2)
C/
C
      CALL ISTKRL(2)
C
      RETURN
C
      END
