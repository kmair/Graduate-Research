      SUBROUTINE LUM1(XB,NXB,N,ML,MI,MR,X,NX)
C
C  TO SET UP THE GRID X WHICH INCLUDES THE POINTS
C
C      XB(I)+(J-1)*((XB(I+1)-XB(I))/(N-1))
C
C  FOR I=1,...,NXB-1 AND J=1,...,N.
C
C  INPUT -
C
C    XB  - THE BASIC POINTS FOR DEFINING THE GRID X.
C          XB MUST BE EITHER MONOTONE INCREASING OR MONOTONE DECREASING.
C          ANY MULTIPLICITIES IN XB ARE IGNORED.
C    NXB - THE NUMBER OF POINTS IN XB.
C    N   - THE NUMBER OF POINTS TO BE USED IN THE GRID
C          X FROM XB(I) TO XB(I+1), FOR I=1,...,NXB-1.
C    ML  - THE MULTIPLICITY OF XB(1) IN X.
C    MI  - THE MULTIPLICITY OF ALL X POINTS STRICTLY
C          BETWEEN XB(1) AND XB(NXB).
C    MR  - THE MULTIPLICITY OF XB(NXB) IN X.
C
C  OUTPUT -
C
C    X  - THE GRID X.
C    NX - THE NUMBER OF MESH POINTS IN X.
C
C           NX = ML +
C                (SUM(I=1,...,NXB-1,XB(I).NE.XB(I+1))(N-1) -1)*MI +
C                MR.
C
C  SCRATCH SPACE ALLOCATED - NONE.
C
C  ERROR STATES -
C
C    1 - NXB.LT.2.
C    2 - ONE OF (ML,MI,MR) IS NEGATIVE.
C    3 - XB IS NOT MONOTONE.
C    4 - N.LT.2.
C
      REAL XB(NXB),X(1)
C     REAL X(NX)
      INTEGER N
C
      REAL DIR
C
C/6S
C     IF (NXB.LT.2) CALL SETERR(16H LUM1 - NXB.LT.2,16,1,2)
C     IF (ML.LT.0.OR.MI.LT.0.OR.MR.LT.0) CALL SETERR
C    1   (37H LUM1 - ONE OF (ML,MI,MR) IS NEGATIVE,37,2,2)
C/7S
      IF (NXB.LT.2) CALL SETERR(' LUM1 - NXB.LT.2',16,1,2)
      IF (ML.LT.0.OR.MI.LT.0.OR.MR.LT.0) CALL SETERR
     1   (' LUM1 - ONE OF (ML,MI,MR) IS NEGATIVE',37,2,2)
C/
C
      DIR=XB(NXB)-XB(1)
C/6S
C     IF (DIR.EQ.0.0E0) CALL SETERR
C    1  (26H LUM1 - XB IS NOT MONOTONE,26,3,2)
C/7S
      IF (DIR.EQ.0.0E0) CALL SETERR
     1  (' LUM1 - XB IS NOT MONOTONE',26,3,2)
C/
C
      MLT=ML
      MRT=0
      NXI=0
      NXBM1=NXB-1
C
C ... SET THE MESH UP, INTERVAL BY INTERVAL.
C
      DO 10 I=1,NXBM1
        IF (XB(I).EQ.XB(I+1)) GO TO 10
C/6S
C       IF ((XB(I+1)-XB(I))*(DIR/ABS(DIR)).LT.0.0E0) CALL SETERR
C    1    (26H LUM1 - XB IS NOT MONOTONE,26,3,2)
C       IF (N.LT.2) CALL SETERR(14H LUM1 - N.LT.2,14,4,2)
C/7S
        IF ((XB(I+1)-XB(I))*(DIR/ABS(DIR)).LT.0.0E0) CALL SETERR
     1    (' LUM1 - XB IS NOT MONOTONE',26,3,2)
        IF (N.LT.2) CALL SETERR(' LUM1 - N.LT.2',14,4,2)
C/
C
        IF (XB(I+1).EQ.XB(NXB)) MRT=MR
        CALL UM1(XB(I),XB(I+1),N,MLT,MI,MRT,X(NXI+1),NXT)
        MLT=MI
        NXI=NXI+NXT
 10     CONTINUE
C
      NX=NXI
C
      RETURN
C
      END
