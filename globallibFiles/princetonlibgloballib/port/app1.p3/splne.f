      SUBROUTINE SPLNE(K,T,N,A,
     1                  X,NX,
     2                  FX)
C
C  TO EVALUATE THE B-SPLINE
C
C            FX(IX)=F(X(IX))
C
C  FOR IX=1,...,NX, WHERE
C
C        F(X)=SUM(I=1,...,N-K)(A(I)*B(I)(X)).
C
C  IF MANY SMALL NX CALLS TO SPLNE ARE ANTICIPATED, IT WOULD BE CHEAPER
C  TO USE SPLN2, SINCE SPLN2 SAVES INTERMEDIATE RESULTS
C  FROM ONE CALL TO THE NEXT.
C
C  INPUT
C
C    K   - THE ORDER OF THE B-SPLINES TO BE USED.
C          2.LE.K IS ASSUMED.
C    T   - THE B-SPLINE MESH.
C    N   - THE NUMBER OF POINTS IN THE MESH T.
C    A   - THE B-SPLINE COEFFICIENTS, N-K OF THEM.
C    X   - POINTS OF EVALUATION FOR THE B-SPLINE.
C          X MONOTONE INCREASING WOULD HELP, BUT IT IS NOT NECESSARY.
C    NX  - THE NUMBER OF POINTS IN X.
C
C  OUTPUT
C
C    FX - THE VALUES OF F.
C
C  SCRATCH SPACE ALLOCATED - 6*K REAL WORDS.
C
C  ERROR STATES -
C
C    1 - K.LT.2.
C    2 - N.LE.K.
C    3 - NX.LT.1.
C    4 - T IS NOT MONOTONE INCREASING.
C
      REAL T(N),A(1),X(NX),FX(NX)
C     REAL A(N-K)
C
      INTEGER ID(1)
      LOGICAL A3PLNE
C
      COMMON /CSTAK/DS
      DOUBLE PRECISION DS(500)
      REAL WS(1)
      EQUIVALENCE (DS(1),WS(1))
C
      DATA ID(1) /0/
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (K.LT.2)
C    1   CALL SETERR(15H SPLNE - K.LT.2,15,1,2)
C     IF (N.LE.K)
C    1   CALL SETERR(15H SPLNE - N.LE.K,15,2,2)
C     IF (NX.LT.1)
C    1   CALL SETERR(16H SPLNE - NX.LT.1,16,3,2)
C/7S
      IF (K.LT.2)
     1   CALL SETERR(' SPLNE - K.LT.2',15,1,2)
      IF (N.LE.K)
     1   CALL SETERR(' SPLNE - N.LE.K',15,2,2)
      IF (NX.LT.1)
     1   CALL SETERR(' SPLNE - NX.LT.1',16,3,2)
C/
C
C ... ALLOCATE SCRATCH SPACE.
C
      IADIFF=ISTKGT(6*K,3)
C
      ILEFT=(N+1)/2
      ILO=0
C/6S
C     IF (.NOT.A3PLNE(K,T,N,A,X,NX,ID,1,FX,3*K,WS(IADIFF),ILO,ILEFT,
C    1                IADIFF+3*K))
C    2  CALL SETERR(37H SPLNE - T IS NOT MONOTONE INCREASING,37,4,2)
C/7S
      IF (.NOT.A3PLNE(K,T,N,A,X,NX,ID,1,FX,3*K,WS(IADIFF),ILO,ILEFT,
     1                IADIFF+3*K))
     2  CALL SETERR(' SPLNE - T IS NOT MONOTONE INCREASING',37,4,2)
C/
C
      CALL ISTKRL(1)
C
      RETURN
C
      END
