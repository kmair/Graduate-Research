      SUBROUTINE DMNPB(X,NX,N,K,T,NT)
C
C
C TO CREATE A B-SPLINE MESH FROM AN ARRAY OF FITTING POINTS.
C
C METHOD - USE AT AT LEAST N FITTING POINTS X IN EACH MESH INTERVA
C
C MNEMONIC - DOUBLE PRECISION MESH GENERATOR
C            USING N POINTS PER INTERVAL, FOR A B-SPLINE.
C
C INPUT -
C
C    X  - THE MONOTONE INCREASING ARRAY OF FITTING POINTS.
C         ANY MULTIPLICITIES IN X WILL BE IGNORED.
C    NX - THE NUMBER OF POINTS IN THE ARRAY X.
C    N  - THE NUMBER OF POINTS X TO BE USED IN EACH MESH INTERVAL.
C         N=2 GIVES A MESH SUITABLE FOR INTERPOLATION OF FUNCTION
C         VALUES AT THE X(I), I=1,...,NX.
C         THE LARGER N IS, THE SMOOTHER THE FIT WILL BE.
C    K  - THE ORDER OF THE B-SPLINE MESH,
C         THE MULTIPLICITY FOR THE FIRST AND LAST MESH POINTS.
C
C OUTPUT -
C
C    T   - THE MESH OF POINTS TAKEN FROM THE ARRAY X.
C    NT  - THE NUMBER OF POINTS IN THE MESH.
C
C          IF ( NDX .GE. MAX(2*N,N*(K+1-NDX)+2*NDX-K)-1 )
C
C            NT = 2*K + 1 +(NDX-MAX(2*N,N*(K+1-NDX)+2*NDX-K)+1)/(N
C
C          OTHERWISE
C
C            NT = 2*K
C
C          WHERE NDX IS THE NUMBER OF DISTINCT POINTS IN THE ARRAY
C
C ERROR STATES -
C
C   1 - NX .LT. 2.
C   2 - N .LT. 2
C   3 - K .LT. 1.
C   4 - THE ARRAY X IS NOT MONOTONE INCREASING.
C   5 - THE NUMBER OF DISTINCT POINTS IN THE ARRAY X IS ONE.
C
C STORAGE ALLOCATED - NONE.
C
      INTEGER NX
      INTEGER N, K, NT
      DOUBLE PRECISION X(NX), T(1)
      INTEGER NL, NR, I, NDX, MAX0
C/6S
C     IF (NX .LT. 2) CALL SETERR(15HDMNPB - NX.LT.2, 15, 1, 2)
C     IF (N .LT. 2) CALL SETERR(14HDMNPB - N.LT.2, 14, 2, 2)
C     IF (K .LT. 1) CALL SETERR(14HDMNPB - K.LT.1, 14, 3, 2)
C/7S
      IF (NX .LT. 2) CALL SETERR('DMNPB - NX.LT.2', 15, 1, 2)
      IF (N .LT. 2) CALL SETERR('DMNPB - N.LT.2', 14, 2, 2)
      IF (K .LT. 1) CALL SETERR('DMNPB - K.LT.1', 14, 3, 2)
C/
      NDX = 1
      DO  1 I = 2, NX
C/6S
C        IF (X(I) .LT. X(I-1)) CALL SETERR(
C    1      36HDMNPB - X IS NOT MONOTONE INCREASING, 36, 4, 2)
C/7S
         IF (X(I) .LT. X(I-1)) CALL SETERR(
     1      'DMNPB - X IS NOT MONOTONE INCREASING', 36, 4, 2)
C/
         IF (X(I) .GT. X(I-1)) NDX = NDX+1
   1     CONTINUE
C/6S
C     IF (NDX .EQ. 1) CALL SETERR(
C    1   37HDMNPB - ONLY ONE POINT IN THE ARRAY X, 37, 5, 2)
C/7S
      IF (NDX .EQ. 1) CALL SETERR(
     1   'DMNPB - ONLY ONE POINT IN THE ARRAY X', 37, 5, 2)
C/
      IF (N .LE. 2 .OR. NDX .LT. 2*K+3) GOTO 2
         NL = N
         NR = N
         GOTO  3
   2     NL = (N*(K+1-NDX)+2*NDX-K)/2
         NR = N*(K+1-NDX)+2*NDX-K-NL
         NL = MAX0(NL, N)
         NR = MAX0(NR, N)
   3  CALL DMNP1(X, NX, NL, N, NR, K, T, NT)
      RETURN
      END
