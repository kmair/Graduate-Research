      SUBROUTINE UM1(A,B,NAB,MA,MI,MB,X,NX)
C
C  TO SET UP THE MESH X(1),...,X(NX) ON (A,B) WHICH INCLUDES THE
C  POINTS A + (I-1)*((B-A)/(NAB-1)), I=1,...,NAB WITH MULTIPLICITIES
C  MA FOR I=1, MI FOR 1.LT.I.LT.NAB AND MB FOR I=NAB.
C
C  INPUT -
C
C    A   - LEFT HAND END POINT.
C    B   - RIGHT HAND END POINT.
C    NAB - NUMBER OF POINTS TO BE USED ON (A,B).
C    MA  - THE MULTIPLICITY OF A IN X.
C    MI  - THE MULTIPLICITY OF ALL INTERIOR POINTS OF X.
C    MB  - THE MULTIPLICITY OF B IN X.
C
C  OUTPUT -
C
C    X  - THE MESH X.
C    NX - THE NUMBER OF MESH POINTS IN X,
C
C            NX = MA + MI*(NAB-2) + MB.
C
C  SCRATCH SPACE ALLOCATED - NONE.
C
C  ERROR STATES -
C
C    1 - A=B.
C    2 - NAB.LT.2.
C    3 - ONE OF (MA,MI,MB) IS NEGATIVE.
C
      REAL A,B,X(1)
C     REAL X(NX)
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (A.EQ.B) CALL SETERR(10H UM1 - A=B,10,1,2)
C     IF (NAB.LT.2) CALL SETERR(15H UM1 - NAB.LT.2,15,2,2)
C     IF (MA.LT.0.OR.MI.LT.0.OR.MB.LT.0) CALL SETERR
C    1   (36H UM1 - ONE OF (MA,MI,MB) IS NEGATIVE,36,3,2)
C/7S
      IF (A.EQ.B) CALL SETERR(' UM1 - A=B',10,1,2)
      IF (NAB.LT.2) CALL SETERR(' UM1 - NAB.LT.2',15,2,2)
      IF (MA.LT.0.OR.MI.LT.0.OR.MB.LT.0) CALL SETERR
     1   (' UM1 - ONE OF (MA,MI,MB) IS NEGATIVE',36,3,2)
C/
C
      NXI=0
C
C ... SET THE LEFT HAND END POINT.
C
      IF (MA.EQ.0) GO TO 20
      DO 10 I=1,MA
         NXI=NXI+1
 10      X(NXI)=A
C
C ... SET THE INTERIOR POINTS.
C
 20   IF (NAB.EQ.2.OR.MI.EQ.0) GO TO 40
      NABM1=NAB-1
      DO 30 I=2,NABM1
         DO 30 J=1,MI
            NXI=NXI+1
 30         X(NXI)=A+FLOAT(I-1)*((B-A)/FLOAT(NAB-1))
C
C ... SET THE RIGHT HAND END POINT.
C
 40   IF (MB.EQ.0) GO TO 60
      DO 50 I=1,MB
         NXI=NXI+1
 50      X(NXI)=B
 60   NX=NXI
C
      RETURN
C
      END
