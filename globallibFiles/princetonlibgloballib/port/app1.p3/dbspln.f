      SUBROUTINE DBSPLN(K,T,N,
     1                  X,NX,ILEFT,
     2                  BX)
C
C  TO OBTAIN ALL NON-ZERO BASIS SPLINES,
C
C           BX(IX,I) = B(ILEFT+I-K) (X(IX))
C
C  FOR IX=1,...,NX, AND I=1,...,K.
C
C  INPUT
C
C    K      - THE ORDER OF THE B-SPLINES TO BE USED.
C             2.LE.K IS ASSUMED.
C    T      - THE B-SPLINE MESH.
C    N      - THE NUMBER OF POINTS IN THE MESH T.
C    X      - POINTS OF EVALUATION FOR THE B-SPLINE.
C    NX     - THE NUMBER OF POINTS IN X.
C    ILEFT  - ALL POINTS X(IX) ARE ASSUMED TO LIE IN THE SAME INTERVAL
C             (T(ILEFT),T(ILEFT+1)).
C
C  OUTPUT
C
C    BX - THE BASIS SPLINES.
C
C  SCRATCH SPACE ALLOCATED - 3*K DOUBLE PRECISION WORDS.
C
C  ERROR STATES -
C
C     1 - K.LT.2.
C     2 - N.LE.K.
C     3 - NX.LT.1.
C     4 - ILEFT.LT.1.
C     5 - ILEFT.GE.N.
C     6 - T(ILEFT)=T(ILEFT+1).
C     7 - ALL X ARE NOT IN THE SAME ILEFT INTERVAL.
C     8 - T IS NOT MONOTONE INCREASING.
C
      DOUBLE PRECISION T(N),X(NX),BX(NX,K)
C
      INTEGER ID(1)
      LOGICAL D3PLN8
C
      DATA ID(1) /0/
C
C ... CHECK THE INPUT.
C
C/6S
C     IF (K.LT.2) CALL SETERR(15HDBSPLN - K.LT.2,15,1,2)
C     IF (N.LE.K) CALL SETERR(15HDBSPLN - N.LE.K,15,2,2)
C     IF (NX.LT.1) CALL SETERR(16HDBSPLN - NX.LT.1,16,3,2)
C     IF (ILEFT.LT.1) CALL SETERR(19HDBSPLN - ILEFT.LT.1,19,4,2)
C     IF (ILEFT.GE.N) CALL SETERR(19HDBSPLN - ILEFT.GE.N,19,5,2)
C     IF (T(ILEFT).EQ.T(ILEFT+1)) CALL SETERR
C    1  (28HDBSPLN - T(ILEFT)=T(ILEFT+1),28,6,2)
C/7S
      IF (K.LT.2) CALL SETERR('DBSPLN - K.LT.2',15,1,2)
      IF (N.LE.K) CALL SETERR('DBSPLN - N.LE.K',15,2,2)
      IF (NX.LT.1) CALL SETERR('DBSPLN - NX.LT.1',16,3,2)
      IF (ILEFT.LT.1) CALL SETERR('DBSPLN - ILEFT.LT.1',19,4,2)
      IF (ILEFT.GE.N) CALL SETERR('DBSPLN - ILEFT.GE.N',19,5,2)
      IF (T(ILEFT).EQ.T(ILEFT+1)) CALL SETERR
     1  ('DBSPLN - T(ILEFT)=T(ILEFT+1)',28,6,2)
C/
C
      DO 10 IX=1,NX
C/6S
C       IF (T(ILEFT).GT.X(IX).OR.X(IX).GT.T(ILEFT+1)) CALL SETERR
C    1    (49HDBSPLN - ALL X ARE NOT IN THE SAME ILEFT INTERVAL,49,7,2)
C       IF (X(IX).EQ.T(ILEFT+1).AND.T(ILEFT+1).LT.T(N)) CALL SETERR
C    1    (49HDBSPLN - ALL X ARE NOT IN THE SAME ILEFT INTERVAL,49,7,2)
C/7S
        IF (T(ILEFT).GT.X(IX).OR.X(IX).GT.T(ILEFT+1)) CALL SETERR
     1    ('DBSPLN - ALL X ARE NOT IN THE SAME ILEFT INTERVAL',49,7,2)
        IF (X(IX).EQ.T(ILEFT+1).AND.T(ILEFT+1).LT.T(N)) CALL SETERR
     1    ('DBSPLN - ALL X ARE NOT IN THE SAME ILEFT INTERVAL',49,7,2)
C/
 10     CONTINUE
C
      IFA=ISTKGT(3*K,4)
C
C/6S
C     IF (.NOT.D3PLN8(K,T,N,X,NX,ILEFT,ID,1,BX,IFA))
C    1  CALL SETERR(37HDBSPLN - T IS NOT MONOTONE INCREASING,37,8,2)
C/7S
      IF (.NOT.D3PLN8(K,T,N,X,NX,ILEFT,ID,1,BX,IFA))
     1  CALL SETERR('DBSPLN - T IS NOT MONOTONE INCREASING',37,8,2)
C/
C
      CALL ISTKRL(1)
C
      RETURN
C
      END
