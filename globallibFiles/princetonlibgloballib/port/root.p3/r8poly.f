      REAL FUNCTION R8POLY(NN,PT,ETA,INFIN,SMALNO,BASE)
C RETURNS A SCALE FACTOR TO MULTIPLY THE COEFFICIENTS OF THE
C POLYNOMIAL. THE SCALING IS DONE TO AVOID OVERFLOW AND TO AVOID
C UNDETECTED UNDERFLOW INTERFERING WITH THE CONVERGENCE
C CRITERION.  THE FACTOR IS A POWER OF THE BASE.
C PT - MODULUS OF COEFFICIENTS OF P
C ETA,INFIN,SMALNO,BASE - CONSTANTS DESCRIBING THE
C FLOATING POINT ARITHMETIC.
      REAL PT(1),ETA,INFIN,SMALNO,BASE,HI,LO,
     1    MAX,MIN,X,SC,SQRT,ALOG
C FIND LARGEST AND SMALLEST MODULI OF COEFFICIENTS.
      HI = SQRT(INFIN)
      LO = SMALNO/ETA
      MAX = 0.0E0
      MIN = INFIN
      DO 10 I = 1,NN
          X = PT(I)
          IF (X .GT. MAX) MAX = X
          IF (X .NE. 0.0E0 .AND. X.LT.MIN) MIN = X
   10 CONTINUE
C SCALE ONLY IF THERE ARE VERY LARGE OR VERY SMALL COMPONENTS.
      R8POLY = 1.0E0
      IF (MIN .GE. LO .AND. MAX .LE. HI) RETURN
      X = LO/MIN
      IF (X .GT. 1.0E0) GO TO 20
          SC = 1.0E0/(SQRT(MAX)*SQRT(MIN))
          GO TO 30
   20 SC = X
      IF (INFIN/SC .GT. MAX) SC = 1.0E0
   30 L = ALOG(SC)/ALOG(BASE) + .500
      R8POLY = BASE**L
      RETURN
      END
