      SUBROUTINE R9POLY(L3,ZR,ZI,CONV,PR,PI,HR,HI,QPR,QPI,QHR,QHI)
C CARRIES OUT THE THIRD STAGE ITERATION.
C L3 - LIMIT OF STEPS IN STAGE 3.
C ZR,ZI   - ON ENTRY CONTAINS THE INITIAL ITERATE, IF THE
C ITERATION CONVERGES IT CONTAINS THE FINAL ITERATE
C ON EXIT.
C CONV    -  .TRUE. IF ITERATION CONVERGES
C COMMON AREA
      COMMON/P88PLY/SR,SI,TR,TI,PVR,PVI,ARE,MRE,ETA,INFIN,NN
C
      REAL SR,SI,TR,TI,PVR,PVI,ARE,MRE,ETA,INFIN,
     1    PR(1),PI(1),HR(1),HI(1),QPR(1),QPI(1),QHR(1),QHI(1)
      REAL ZR,ZI,MP,MS,OMP,RELSTP,R1,R2,CR1MOD,SQRT
      REAL R3POLY,TP
      LOGICAL CONV,B,BOOL
      CONV = .FALSE.
      B = .FALSE.
      SR = ZR
      SI = ZI
C MAIN LOOP FOR STAGE THREE
      DO 60 I = 1,L3
C EVALUATE P AT S AND TEST FOR CONVERGENCE.
          CALL R7POLY(NN,SR,SI,PR,PI,QPR,QPI,PVR,PVI)
          MP = CR1MOD(PVR,PVI)
          MS = CR1MOD(SR,SI)
          IF (MP .GT. 20.0E0*R3POLY(NN,QPR,QPI,MS,MP,ARE,MRE))
     1       GO TO 10
C POLYNOMIAL VALUE IS SMALLER IN VALUE THAN A BOUND ON THE ERROR
C IN EVALUATING P, TERMINATE THE ITERATION.
               CONV = .TRUE.
               ZR = SR
               ZI = SI
               RETURN
   10     IF (I .EQ. 1) GO TO 40
               IF (B .OR. MP .LT.OMP .OR. RELSTP .GE. .05E0)
     1            GO TO 30
C ITERATION HAS STALLED. PROBABLY A CLUSTER OF ZEROS. DO 5 FIXED
C SHIFT STEPS INTO THE CLUSTER TO FORCE ONE ZERO TO DOMINATE.
                    TP = RELSTP
                    B = .TRUE.
                    IF (RELSTP .LT. ETA) TP = ETA
                    R1 = SQRT(TP)
                    R2 = SR*(1.0E0+R1)-SI*R1
                    SI = SR*R1+SI*(1.0E0+R1)
                    SR = R2
                    CALL R7POLY(NN,SR,SI,PR,PI,QPR,QPI,PVR,PVI)
                    DO 20 J = 1,5
                         CALL R1POLY(BOOL,HR,HI,QHR,QHI)
                         CALL R5POLY(BOOL,HR,HI,QPR,QPI,QHR,QHI)
   20               CONTINUE
      OMP = INFIN
                    GO TO 50
C EXIT IF POLYNOMIAL VALUE INCREASES SIGNIFICANTLY.
   30          IF (MP*.1E0 .GT. OMP) RETURN
   40     OMP = MP
C CALCULATE NEXT ITERATE.
   50     CALL R1POLY(BOOL,HR,HI,QHR,QHI)
          CALL R5POLY(BOOL,HR,HI,QPR,QPI,QHR,QHI)
          CALL R1POLY(BOOL,HR,HI,QHR,QHI)
          IF (BOOL) GO TO 60
          RELSTP = CR1MOD(TR,TI)/CR1MOD(SR,SI)
          SR = SR+TR
          SI = SI+TI
   60 CONTINUE
      RETURN
      END
