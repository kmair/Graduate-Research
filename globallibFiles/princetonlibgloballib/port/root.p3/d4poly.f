      SUBROUTINE D4POLY(L2,ZR,ZI,CONV,PR,PI,HR,HI,QPR,QPI,
     1    QHR,QHI,SHR,SHI)
C COMPUTES L2 FIXED-SHIFT H POLYNOMIALS AND TESTS FOR
C
C CONVERGENCE.
C INITIATES A VARIABLE-SHIFT ITERATION AND RETURNS WITH THE
C APPROXIMATE ZERO IF SUCCESSFUL.
C L2 - LIMIT OF FIXED SHIFT STEPS
C ZR,ZI - APPROXIMATE ZERO IF CONV IS .TRUE.
C CONV  - LOGICAL INDICATING CONVERGENCE OF STAGE 3 ITERATION
C COMMON AREA
      COMMON/P99PLY/SR,SI,TR,TI,PVR,PVI,ARE,MRE,ETA,INFIN,NN
C
      DOUBLE PRECISION SR,SI,TR,TI,PVR,PVI,ARE,MRE,ETA,INFIN,
     1    PR(1),PI(1),HR(1),HI(1),QPR(1),QPI(1),QHR(1),
     2    QHI(1),SHR(1),SHI(1)
      DOUBLE PRECISION ZR,ZI,OTR,OTI,SVSR,SVSI,CD1MOD
          LOGICAL CONV,TEST,PASD,BOOL
      N = NN-1
C EVALUATE P AT S.
      CALL D7POLY(NN,SR,SI,PR,PI,QPR,QPI,PVR,PVI)
      TEST = .TRUE.
      PASD = .FALSE.
C CALCULATE FIRST T = -P(S)/H(S).
      CALL D1POLY(BOOL,HR,HI,QHR,QHI)
C MAIN LOOP FOR ONE SECOND STAGE STEP.
      DO 50 J = 1,L2
          OTR = TR
          OTI = TI
C COMPUTE NEXT H POLYNOMIAL AND NEW T.
          CALL D5POLY(BOOL,HR,HI,QPR,QPI,QHR,QHI)
          CALL D1POLY(BOOL,HR,HI,QHR,QHI)
          ZR = SR+TR
          ZI = SI+TI
C TEST FOR CONVERGENCE UNLESS STAGE 3 HAS FAILED ONCE OR THIS
C IS THE LAST H POLYNOMIAL .
          IF ( BOOL .OR. .NOT. TEST .OR. J .EQ. L2) GO TO 50
          IF (CD1MOD(TR-OTR,TI-OTI) .GE. .5D0*CD1MOD(ZR,ZI)) GO TO 40
               IF (.NOT. PASD) GO TO 30
C THE WEAK CONVERGENCE TEST HAS BEEN PASSED TWICE, START THE
C THIRD STAGE ITERATION, AFTER SAVING THE CURRENT H POLYNOMIAL
C AND SHIFT.
                    DO 10 I = 1,N
                         SHR(I) = HR(I)
                         SHI(I) = HI(I)
   10               CONTINUE
                    SVSR = SR
                    SVSI = SI
               CALL D9POLY(10,ZR,ZI,CONV,PR,PI,HR,HI,QPR,QPI,QHR,QHI)
                    IF (CONV) RETURN
C THE ITERATION FAILED TO CONVERGE. TURN OFF TESTING AND RESTORE
C H,S,PV AND T.
                    TEST = .FALSE.
                    DO 20 I = 1,N
                         HR(I) = SHR(I)
                         HI(I) = SHI(I)
   20               CONTINUE
                    SR = SVSR
                    SI = SVSI
                    CALL D7POLY(NN,SR,SI,PR,PI,QPR,QPI,PVR,PVI)
                    CALL D1POLY(BOOL,HR,HI,QHR,QHI)
                    GO TO 50
   30          PASD = .TRUE.
               GO TO 50
   40     PASD = .FALSE.
   50 CONTINUE
C ATTEMPT AN ITERATION WITH FINAL H POLYNOMIAL FROM SECOND STAGE.
      CALL D9POLY(10,ZR,ZI,CONV,PR,PI,HR,HI,QPR,QPI,QHR,QHI)
      RETURN
      END
