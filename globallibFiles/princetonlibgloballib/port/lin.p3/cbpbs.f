              SUBROUTINE  CBPBS(N,ML,G,IG,B,IB,NB)
C
C THIS SUBROUTINE SOLVES DTX = B WHERE T IS A
C UNIT UPPER TRIANGULAR MATRIX AND D IS A DIAGONAL
C MATRIX
C
C INPUT PARAMETERS
C N         THE ORDER OF THE SYSTEM
C G         AN IG X N ARRAY CONTAINING THE UPPERTRIANGULAR
C           MATRIX T WHERE G(1+J-I,I)=T(I,J)
C           AND THE DIAGONAL MATRIX D IN ITS FIRST ROW
C IG        ROW DIMENSION OF G
C B         THE RIGHT HAND SIDE;OVERWRITTEN ON OUTPUT
C IB        ROW DIMENSION OF THE B MATRIX
C NB        NUMBER OF RIGHT HAND SIDES
C ML        THE NUMBER OF NONZERO DIAGONALS OF T
C OUTPUT PARAMETERS
C B         A VECTOR CONAINING THE SOLUTION X
C SCRATCH SPACE NEEDED -NONE
C ERROR CONDITIONS
C   1       N.LT.1        FATAL
C   2       ML.LT.1       FATAL
C   4       IB.LT.N       FATAL
C  5        NB.LT.1       FATAL
C   3       ML.GT.IG      FATAL
C   10+K    SINGULAR G RANK K  RECOVERABLE
        INTEGER N,IG,ML
        COMPLEX G(IG,N),B(IB,NB)
        COMPLEX X
C/6S
C       IF (N.LT.1) CALL SETERR(13H CBPBS-N.LT.1,13,1,2)
C       IF (IG.LT.1) CALL SETERR(14H CBPBS-IG.LT.1,14,2,2)
C       IF (ML.LT.1) CALL SETERR(14H CBPBS-ML.LT.1,14,2,2)
C       IF (ML.GT.IG) CALL SETERR(15H CBPBS-ML.GT.IG,15,3,2)
C       IF(IB.LT.N) CALL SETERR(14H CBPBS-IB.LT.N,14,4,2)
C       IF(NB.LT.1) CALL SETERR(14H CBPBS-NB.LT.1,14,5,2)
C/7S
        IF (N.LT.1) CALL SETERR(' CBPBS-N.LT.1',13,1,2)
        IF (IG.LT.1) CALL SETERR(' CBPBS-IG.LT.1',14,2,2)
        IF (ML.LT.1) CALL SETERR(' CBPBS-ML.LT.1',14,2,2)
        IF (ML.GT.IG) CALL SETERR(' CBPBS-ML.GT.IG',15,3,2)
        IF(IB.LT.N) CALL SETERR(' CBPBS-IB.LT.N',14,4,2)
        IF(NB.LT.1) CALL SETERR(' CBPBS-NB.LT.1',14,5,2)
C/
        CALL ENTER(1)
        L=1
        NP1=N+1
        DO 100 II=1,N
            I=NP1-II
            IF (CABS1(G(1,I)).NE.0.E0) GO TO 40
C/6S
C              CALL SETERR(22H CBPBS-SINGULAR MATRIX,22,9+I,1)
C/7S
               CALL SETERR(' CBPBS-SINGULAR MATRIX',22,9+I,1)
C/
               GO TO 150
 40     CONTINUE
        DO 70 J=1,NB
            B(I,J)=B(I,J)/G(1,I)
            IF (L.LT.2) GO TO 70
            X=B(I,J)
            KB=I
            DO 60 K=2,L
               KB=KB+1
               X=X-G(K,I)*B(KB,J)
 60          CONTINUE
            B(I,J)=X
 70       CONTINUE
 80         L=MIN0(L+1,ML)
 100     CONTINUE
 150     CALL LEAVE
         RETURN
         END
