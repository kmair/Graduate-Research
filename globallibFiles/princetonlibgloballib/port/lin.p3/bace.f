      SUBROUTINE   BACE(N, ML, M, G, IG,GL,IGL,INTER, MU,COND)
      INTEGER IG, N
      INTEGER INTER(N),IGL
      DOUBLE PRECISION D(500)
      REAL R(1000)
      REAL GL(IGL,N)
      INTEGER M, ML
      REAL G(IG, N), COND
      INTEGER  MU, IZ, IERR, ISTKGT, NERROR
      REAL ANORM, R1MACH
      COMMON /CSTAK/ D
      EQUIVALENCE(D(1),R(1))
C BAND CONDITION ESTIMATOR AND DECOMPOSER
C INPUT PARAMETERS
C N        NUMBER OF ROWS IN A
C ML       NUMBER OF NONZERO DIAGONAL ON AND BELOW THE DIAGONAL
C M        NUMBER OF NONZERO DIAGONALS ABOVE THE DIAGONAL
C G        ARRAY INTO WHICH THE BAND MATRIX A IS PACKED
C          G(ML+I-J,I)=A(I,J)
C IG       ROW DIMENSION OF G
C IGL      ROW DIMENSION OF GL, AT LEAST ML-1
C OUTPUT PARAMETERS
C G        THE U FROM THE LU DECOMPOSITION OF G
C GL       THE L FROM THE LU DECOMPOSITION OF G
C          AN IGL X N ARRAY
C INTER    THE INTERCHANGES NEEDED TO PERFORM THE DECOMPOSITION
C         AN INTEGER VECTOR OF LENGTH N
C COND     A LOWER ESTIMATE OF THE CONDITION NUMBER OF A
C          COND RANGES FROM 1 TO THE LARGEST NUMBER ON THE MACHINE
C          THE LARGER THE NUMBER THE MORE ILL CONDITIONED THE MATRIX
C          IN GENERAL IF COND IS 10,1 DECIMAL DIGIT IN A LINEAR
C          SYSTEM WILL BE INACCURATE, COND IS 100,2 DECIMAL DIGITS
C          WILL BE INACCURATE,ETC.
C STORAGE ALLOCATED -N REAL LOCATIONS
C ERROR CONDITIONS
C 1   N.LT.1      FATAL
C 2   ML.GT.N     FATAL
C 3   M.LT.ML     FATAL
C 4   IG.LT.M     FATAL
C 5   IGL.LT.ML-1   FATAL
C 10+K SINGULAR MATRIX OF RANK K RECOVERABLE
      CALL ENTER(1)
C/6S
C     IF (N .LT. 1) CALL SETERR(13H  BACE-N.LT.1, 13, 1, 2)
C     IF (ML .GT. N) CALL SETERR(14H  BACE-ML.GT.N, 14, 2, 2)
C     IF (M .LT. ML) CALL SETERR(14H  BACE-M.LT.ML, 14, 3, 2)
C     IF (IG .LT. M) CALL SETERR(14H  BACE-IG.LT.M, 14, 4, 2)
C     IF (IGL.LT.ML-1) CALL SETERR(18H  BACE-IGL.LT.ML-1,18,5,2)
C/7S
      IF (N .LT. 1) CALL SETERR('  BACE-N.LT.1', 13, 1, 2)
      IF (ML .GT. N) CALL SETERR('  BACE-ML.GT.N', 14, 2, 2)
      IF (M .LT. ML) CALL SETERR('  BACE-M.LT.ML', 14, 3, 2)
      IF (IG .LT. M) CALL SETERR('  BACE-IG.LT.M', 14, 4, 2)
      IF (IGL.LT.ML-1) CALL SETERR('  BACE-IGL.LT.ML-1',18,5,2)
C/
      ANORM=BANM(N,ML,M,G,IG)
      CALL   BALU(N, ML, M, G, IG, GL, IGL, INTER, MU, 0.E0)
      IF (NERROR(IERR) .EQ. 0) GOTO 1
C/6S
C     CALL N5ERR(21H BACE-SINGULAR MATRIX,21,IERR,1)
C/7S
      CALL N5ERR(' BACE-SINGULAR MATRIX',21,IERR,1)
C/
         COND = R1MACH(2)
         CALL LEAVE
         RETURN
   1  IZ = ISTKGT(N, 3)
      CALL B4DCE(N, ML, G, IG, GL,IGL, INTER, COND, MU,
     1   ANORM, R(IZ))
      CALL LEAVE
      RETURN
      END
