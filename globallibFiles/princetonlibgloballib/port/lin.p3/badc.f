          SUBROUTINE   BADC(N,ML,M,G,IG,AL,IAL,INT,MU)
C
C THIS SUBROUTINE DETERMINES THE LU DECOMPOSITION OF A
C BANDED UNSYMMETRIC MATRIX USING GAUSSIAN ELIMINATION
C WITH PARTIAL PIVOTING
C INPUT PARAMETERS
C N      THE ORDER OF THE MATRIX
C ML      MAXIMUM NUMBER OF NONZERO ELEMENTS ON OR BELOW
C         THE DIAGONAL IN ANY COLUMN OF THE MATRIX
C M       THE MAXIMUM NUMBER OF NONZERO ELEMENTS IN EACH
C         COLUMN
C G       THE MATRIX A STORED AS AN IG X N ARRAY
C         WITH G(ML+J-I)=A(I,J).I.E. THE LEFTMOST
C         DIAGONAL OF THE BAND MATRIX IS THE FIRST ROW OF G
C IG      ROW DIMENSION OF IG ,MUST NOT BE LESS THAN M
C IAL     ROW DIMENSION OF AL, MUST NOT BE LESS THAN ML
C OUTPUT PARAMETERS
C G       THE UPPER TRIANGULAR BAND FACTOR OF A
C AL      AN IAL X N ARRAY CONTAINING THE LOWER TRIANGULAR
C         BAND FACTOR OF A
C INT     AN INTEGER VECTOR OF LENGTH N RECORDING INTERCHANGES
C          INVOLVED IN GAUSSIAN ELIMINATION
C MU      THE NUMBER OF NONZERO DIAGONALS IN THE UPPER
C         TRIANGULAR BAND FACTOR
C SCRATCH STORAGE ALLOCATED-NONE
C ERROR STATES
C 1   N.LT.1      FATAL
C 2   ML.LT.1     FATAL
C 3   M.LT.ML     FATAL
C 4   IG.LT.M     FATAL
C 5   IAL.LT.ML-1 FATAL
C 10+K SINGULAR MATRIX OF RANK K   RECOVERABLE
           INTEGER IG,IAL
           INTEGER N,ML,M,INT(N)
           REAL G(IG,N),AL(IAL,N)
           REAL EPS, R1MACH, BANM
C
C CHECK FOR INPUT ERRORS
C
C/6S
C      IF (N.LT.1)CALL SETERR(13H  BADC-N.LT.1,13,1,2)
C      IF (ML.LT.1)CALL SETERR(14H  BADC-ML.LT.1,14,2,2)
C      IF (M.LT.ML) CALL SETERR(14H  BADC-ML.LT.M,14,3,2)
C      IF (IG.LT.M) CALL SETERR(14H  BADC-IG.LT.M,14,4,2)
C      IF (IAL.LT.ML-1)CALL SETERR(18H  BADC-IAL.LT.ML-1,18,5,2)
C/7S
       IF (N.LT.1)CALL SETERR('  BADC-N.LT.1',13,1,2)
       IF (ML.LT.1)CALL SETERR('  BADC-ML.LT.1',14,2,2)
       IF (M.LT.ML) CALL SETERR('  BADC-ML.LT.M',14,3,2)
       IF (IG.LT.M) CALL SETERR('  BADC-IG.LT.M',14,4,2)
       IF (IAL.LT.ML-1)CALL SETERR('  BADC-IAL.LT.ML-1',18,5,2)
C/
       CALL ENTER(1)
          EPS=BANM(N,ML,M,G,IG)
          EPS=EPS*R1MACH(4)
          CALL   BALU(N,ML,M,G,IG,AL,IAL,INT,MU,EPS)
C/6S
C         IF(NERROR(IERR).NE.0)
C    1      CALL N5ERR(22H  BADC-SINGULAR MATRIX,22,IERR,1)
C/7S
          IF(NERROR(IERR).NE.0)
     1      CALL N5ERR('  BADC-SINGULAR MATRIX',22,IERR,1)
C/
          CALL LEAVE
          RETURN
          END
