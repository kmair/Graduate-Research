              SUBROUTINE   CBABS(N,G,IG,B,IB,NB,MU)
C
C THIS SUBROUTINE SOLVES TX = B WHERE T IS AN
C UPPER TRIANGULAR BANDED MATRIX
C
C INPUT PARAMETERS
C N         THE ORDER OF THE SYSTEM
C G         AN IG X N ARRAY CONTAINING THE UPPERTRIANGULAR
C           MATRIX T WHERE G(1+J-I,I)=T(I,J)
C IG        ROW DIMENSION OF G
C B         THE RIGHT HAND SIDES;OVERWRITTEN ON OUTPUT
C IB        ROW DIMENSION OF B MATRIX
C NB        NUMBER OF RIGHT HAND SIDES
C MU        THE NUMBER OF NONZERO DIAGONALS OF T
C OUTPUT PARAMETERS
C B         THE SOLUTION MATRIX X
C SCRATCH SPACE NEEDED -NONE
C ERROR CONDITIONS
C   1       N.LT.1        FATAL
C   2       IG.LT.MU      FATAL
C   3       MU.LT.1       FATAL
C   4       IB.LT.N     FATAL
C  5        NB.LT.1       FATAL
C   10+K    SINGULAR G OF RANK K  RECOVERABLE
        INTEGER IG,N,MU
        COMPLEX G(IG,N),B(IB,NB)
        COMPLEX X
C/6S
C       IF (N.LT.1) CALL SETERR(13H CBABS-N.LT.1,13,1,2)
C       IF (IG.LT.MU) CALL SETERR(15H CBABS-IU.LT.MU,15,2,2)
C       IF (MU.LT.1) CALL SETERR(14H CBABS-MU.LT.1,14,3,2)
C       IF (IB.LT.N) CALL SETERR(14H CBABS-IB.LT.N,14,4,2)
C       IF (NB.LT.1) CALL SETERR(14H CBABS-NB.LT.1,14,5,2)
C/7S
        IF (N.LT.1) CALL SETERR(' CBABS-N.LT.1',13,1,2)
        IF (IG.LT.MU) CALL SETERR(' CBABS-IU.LT.MU',15,2,2)
        IF (MU.LT.1) CALL SETERR(' CBABS-MU.LT.1',14,3,2)
        IF (IB.LT.N) CALL SETERR(' CBABS-IB.LT.N',14,4,2)
        IF (NB.LT.1) CALL SETERR(' CBABS-NB.LT.1',14,5,2)
C/
        CALL ENTER(1)
        L=1
        NP1=N+1
        DO 100 II=1,N
            I=NP1-II
            DO 50 J=1,NB
            X=B(I,J)
            IF (L.LT.2) GO TO 30
            KB=I
            DO 20 K=2,L
               KB=KB+1
               X=X-G(K,I)*B(KB,J)
 20         CONTINUE
 30         IF (CABS1(G(1,I)).NE.0.E0) GO TO 40
C/6S
C              CALL SETERR(22H CBABS-SINGULAR MATRIX,22,9+I,1)
C/7S
               CALL SETERR(' CBABS-SINGULAR MATRIX',22,9+I,1)
C/
               GO TO 150
 40         B(I,J)=X/G(1,I)
 50      CONTINUE
            L=MIN0(L+1,MU)
 100     CONTINUE
 150     CALL LEAVE
         RETURN
         END
