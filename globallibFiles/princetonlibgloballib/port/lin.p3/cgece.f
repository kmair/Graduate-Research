      SUBROUTINE CGECE(N, A, IA, INTER, COND)
      INTEGER IA, N, INTER(N)
      COMPLEX A(IA,N)
      REAL COND
      COMMON /CSTAK / D
      DOUBLE PRECISION D(500)
      INTEGER  NERR, ISTKGT, NERROR, IWORK
      REAL CGENM, SNORM, R1MACH
      COMPLEX R(500)
      EQUIVALENCE (R(1), D(1))
C THIS SUBROUTINE CALLS THE DECOMPOSITION ROUTINE AND DETERMINES
C AN ESTIMATE OF THE CONDITION NUMBER OF A COMPLEX GENERAL MATRIX
C INPUT PARAMETERS
C    N        ORDER OF THE PROBLEM
C    A        MATRIX WHOSE CONDITION NUMBER IS REQUIRED
C    IA       ROW DIMENSION OF A
C OUTPUT PARAMETERS
C    A        CONTAINS THE LU DECOMPOSITION OF THE A MATRIX
C    INTER    PIVOT VECTOR PRODUCED BY THE DECOMPOSITION
C    COND     AN ESTIMATE OF THE CONDITION NUMBER OF THE MATRIX A
C EXTRA STORAGE ALLOCATED : N COMPLEX LOCATIONS
C THE SUBROUTINES CGENM, CGELU, AND CG4ECE ARE CALLED
C ERROR CONDITIONS-
C    1     N.LT.1       FATAL
C    2    IA.LT.N       FATAL
      CALL ENTER(1)
C/6S
C     IF (N .LT. 1) CALL SETERR(13HCGECE-N.LT.1 , 13, 1, 2)
C     IF (IA .LT. N) CALL SETERR(13HCGECE-IA.LT.N, 13, 2, 2)
C/7S
      IF (N .LT. 1) CALL SETERR('CGECE-N.LT.1 ', 13, 1, 2)
      IF (IA .LT. N) CALL SETERR('CGECE-IA.LT.N', 13, 2, 2)
C/
      SNORM=CGENM(N,A,IA)
      CALL CGELU(N,A,IA,INTER,0.0)
      IF (NERROR(NERR) .EQ. 0) GOTO 2
         COND = R1MACH(2)
C/6S
C        CALL N5ERR(21HCGECE-SINGULAR MATRIX,21,NERR,1)
C/7S
         CALL N5ERR('CGECE-SINGULAR MATRIX',21,NERR,1)
C/
         GOTO  3
   2     IWORK = ISTKGT(N,5)
         CALL CG4ECE(N,A,IA,SNORM,COND,INTER,R(IWORK))
   3  CALL LEAVE
      RETURN
      END
