         SUBROUTINE   BAFS(N,ML,AL,IAL,INT,B,IB,NB)
C
C THIS SUBROUTINE SOLVES LX=B WHERE L IS A LOWER TRIANGULAR
C BAND MATRIX
C INPUT PARAMETERS
C N      THE ORDER OF THE SYSTEM
C ML     THE NUMBER OF NONZERO ELEMENTS OF L ON AND BELOW
C        THE DIAGONAL,THE ROW DIMENSION OF AL
C AL     AN IAL X N MATRIX CONTAINING THE LOWERTRIANGULAR
C        BAND MATRIX. NOTE THAT AL(I-J,J)=L(I,J)
C IAL    ROW DIMENSION OF AL
C INT    AN INTEGER VECTOR OF LENGTH N COMPUTED BY   BALU
C        RECORDING THE INTERCHANGES PERFORMED ON L
C B      A VECTOR OF LENGTH N CONTAINING THE RIGHT HAND SIDE
C OUTPUT PARAMETERS
C B      THE SOLUTION X
C SCRATCH SPACE ALLOCATED - NONE
C ERROR CONDITIONS
C 1     N.LT.1     FATAL
C 2     ML.LT.1     FATAL
C 3     IAL.LT.ML-1 FATAL
C 4     INT(I)    NOT IN 1,.....N     FATAL
C 5     IB.LT.N          FATAL
C 6     NB.LT.1          FATAL
C
         INTEGER IAL,N,ML,INT(N)
        REAL AL(IAL,N),B(IB,NB)
        REAL X
C CHECK ERROR CONDITIONS
C/6S
C      IF (N.LT.1)CALL SETERR(13H  BAFS-N.LT.1,13,1,2)
C      IF( ML.LT.1)CALL SETERR(14H  BAFS-ML.LT.1,14,2,2)
C      IF (IAL.LT.ML-1)CALL SETERR(18H  BAFS-IAL.LT.ML-1,18,3,2)
C      IF(IB.LT.N) CALL SETERR(14H  BAFS-IB.LT.N,14,5,2)
C      IF(NB.LT.1) CALL SETERR(14H  BAFS-NB.LT.1,14,6,2)
C/7S
       IF (N.LT.1)CALL SETERR('  BAFS-N.LT.1',13,1,2)
       IF( ML.LT.1)CALL SETERR('  BAFS-ML.LT.1',14,2,2)
       IF (IAL.LT.ML-1)CALL SETERR('  BAFS-IAL.LT.ML-1',18,3,2)
       IF(IB.LT.N) CALL SETERR('  BAFS-IB.LT.N',14,5,2)
       IF(NB.LT.1) CALL SETERR('  BAFS-NB.LT.1',14,6,2)
C/
       IF (ML.EQ.1) RETURN
       IF (N.EQ.1)RETURN
       CALL ENTER(1)
       M1=ML-1
       NM1=N-1
       DO 60 K=1,NM1
         KP1=K+1
          I=INT(K)
C/6S
C         IF (I.LT.1.OR.I.GT.N)
C    1       CALL SETERR(26H BAFS-INT(I) NOT IN 1,..,N,26,4,2)
C/7S
          IF (I.LT.1.OR.I.GT.N)
     1       CALL SETERR(' BAFS-INT(I) NOT IN 1,..,N',26,4,2)
C/
          DO 55 J=1,NB
          X=B(I,J)
          IF (I.EQ.K) GO TO 10
C INTERCHANGE ELEMENTS OF B
              B(I,J)=B(K,J)
              B(K,J)=X
 10     CONTINUE
            IEND=M1+K
            IF (N.LT.IEND)IEND=N
            IMK=0
            DO 50 II=KP1,IEND
                 IMK=IMK+1
                 B(II,J)=B(II,J)+AL(IMK,K)*X
 50         CONTINUE
 55       CONTINUE
 60       CONTINUE
 100     CALL LEAVE
          RETURN
         END
