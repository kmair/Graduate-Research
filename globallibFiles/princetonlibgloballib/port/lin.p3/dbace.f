      SUBROUTINE  DBACE(N, ML, M, G, IG,GL,IGL,INTER, MU,COND)
      INTEGER IG, N
      INTEGER INTER(N),IGL
      DOUBLE PRECISION D(500)
      DOUBLE PRECISION GL(IGL,N)
      INTEGER M, ML
      DOUBLE PRECISION G(IG, N), COND
      INTEGER  MU, IZ, IERR, ISTKGT, NERROR
      DOUBLE PRECISION ANORM, D1MACH,DBANM
      COMMON /CSTAK/ D
C BAND CONDITION ESTIMATOR AND DECOMPOSER
C INPUT PARAMETERS
C N        NUMBER OF ROWS IN A
C ML       NUMBER OF NONZERO DIAGONAL ON AND BELOW THE DIAGONAL
C M        NUMBER OF NONZERO DIAGONALS ABOVE THE DIAGONAL
C G        ARRAY INTO WHICH THE BAND MATRIX A IS PACKED
C          G(ML+I-J,I)=A(I,J)
C IG       ROW DIMENSION OF G
C IGL      ROW DIMENSION OF GL, AT LEAST ML-1
C OUTPUT PARAMETERS
C G        THE U FROM THE LU DECOMPOSITION OF G
C GL       THE L FROM THE LU DECOMPOSITION OF G
C          AN IGL X N ARRAY
C INTER    THE INTERCHANGES NEEDED TO PERFORM THE DECOMPOSITION
C         AN INTEGER VECTOR OF LENGTH N
C COND     A LOWER ESTIMATE OF THE CONDITION NUMBER OF A
C          COND RANGES FROM 1 TO THE LARGEST NUMBER ON THE MACHINE
C          THE LARGER THE NUMBER THE MORE ILL CONDITIONED THE MATRIX
C          IN GENERAL IF COND IS 10,1 DECIMAL DIGIT IN A LINEAR
C          SYSTEM WILL BE INACCURATE, COND IS 100,2 DECIMAL DIGITS
C          WILL BE INACCURATE,ETC.
C STORAGE ALLOCATED -N DOUBLE PRECISION LOCATIONS
C ERROR CONDITIONS
C 1   N.LT.1      FATAL
C 2   ML.GT.N     FATAL
C 3   M.LT.ML     FATAL
C 4   IG.LT.M     FATAL
C 5   IGL.LT.ML-1   FATAL
C 10+K SINGULAR MATRIX OF RANK K RECOVERABLE
      CALL ENTER(1)
C/6S
C     IF (N .LT. 1) CALL SETERR(13H DBACE-N.LT.1, 13, 1, 2)
C     IF (ML .GT. N) CALL SETERR(14H DBACE-ML.GT.N, 14, 2, 2)
C     IF (M .LT. ML) CALL SETERR(14H DBACE-M.LT.ML, 14, 3, 2)
C     IF (IG .LT. M) CALL SETERR(14H DBACE-IG.LT.M, 14, 4, 2)
C     IF (IGL.LT.ML-1) CALL SETERR(18H DBACE-IGL.LT.ML-1,18,5,2)
C/7S
      IF (N .LT. 1) CALL SETERR(' DBACE-N.LT.1', 13, 1, 2)
      IF (ML .GT. N) CALL SETERR(' DBACE-ML.GT.N', 14, 2, 2)
      IF (M .LT. ML) CALL SETERR(' DBACE-M.LT.ML', 14, 3, 2)
      IF (IG .LT. M) CALL SETERR(' DBACE-IG.LT.M', 14, 4, 2)
      IF (IGL.LT.ML-1) CALL SETERR(' DBACE-IGL.LT.ML-1',18,5,2)
C/
      ANORM=DBANM(N,ML,M,G,IG)
      CALL  DBALU(N, ML, M, G, IG, GL, IGL, INTER, MU, 0.D0)
      IF (NERROR(IERR) .EQ. 0) GOTO 1
C/6S
C     CALL N5ERR(21HDBACE-SINGULAR MATRIX,21,IERR,1)
C/7S
      CALL N5ERR('DBACE-SINGULAR MATRIX',21,IERR,1)
C/
         COND = D1MACH(2)
         CALL LEAVE
         RETURN
   1  IZ = ISTKGT(N, 4)
      CALL DB4DCE(N, ML, G, IG, GL,IGL, INTER, COND, MU,
     1   ANORM, D(IZ))
      CALL LEAVE
      RETURN
      END
