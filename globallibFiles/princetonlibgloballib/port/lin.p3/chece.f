      SUBROUTINE  CHECE(N, C, INTER, COND)
      INTEGER N
      INTEGER INTER(N)
      COMPLEX C(1)
      REAL COND
      COMMON /CSTAK/ D
      DOUBLE PRECISION D(500)
      COMPLEX DC(500)
      INTEGER IZ, IERR, ISTKGT, NERROR
      REAL ANORM, R1MACH, CHENM
      EQUIVALENCE(DC(1),D(1))
C SYMMETRIC CONDITION ESTIMATION
C INPUT PARMETERS
C N          NUMBER OF ROWS IN A
C C       VECTOR INTO WHICH THE SYMMETRIC MATRIX A IS
C         PACKED BY COLUMNS ACCORDING TO THE FOLLOWING
C         TEMPLATE
C            1
C            2  5
C            3  6  8
C            4  7  9  10
C OUTPUT PARAMETERS
C C          MDM(TRANSPOSE) DECOMPOSITION SUITABLE AS INPUT
C            INTO CHEFBS
C INTER      INTEGER ARRAY OF LENGTH N OF INTERCHANGES PERFORMED
C            DURING DECOMPOSITION
C            SUITABLE OF INPUT INTO CHEFBS
C COND       LOWER BOUND ON THE CONDITION NUMBER OF A
C STORAGE ALLOCATED-  N COMPLEX LOCATIONS
C ERROR CONDITIONS
C 1          N.LT.1
C/6S
C     IF (N .LT. 1) CALL SETERR(12HCHECE-N.LT.1, 12, 1, 2)
C/7S
      IF (N .LT. 1) CALL SETERR('CHECE-N.LT.1', 12, 1, 2)
C/
      CALL ENTER(1)
      ANORM=CHENM(N,C)
      CALL  CHEMD(N, C, INTER,0.0)
      IF (NERROR(IERR) .EQ. 0) GOTO 1
         COND = R1MACH(2)
         CALL LEAVE
         RETURN
   1  IZ = ISTKGT(N, 5)
      CALL C4HCE(N, C, COND, INTER, ANORM, DC(IZ))
      CALL LEAVE
      RETURN
      END
