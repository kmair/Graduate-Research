      SUBROUTINE DBPCE(N,ML,G,IG, COND)
C
C THIS SUBROUTINE DETERMINES A LOWER BOUND ON THE CONDITION
C NUMBER OF A BAND POSITIVE DEFINITE MATRIX G
C
C INPUT PARAMTERS
C    N      ORDER OF THE MATRIX G
C    G      BAND POSITIVE DEFINITE MATRIX WHOSE CONDITION
C           NUMBER IS REQUIRED
C    ML     THE NUMBER OF BANDS ON AND BELOW THE DIAGONAL
C    IG     ROW DIMENSION OF G - MUST BE .GE.ML
C
C OUTPUT PARAMETERS
C    G      A DECOMPOSITION OF THE MATRIX G
C    COND   AN ESTIMATE OF THE CONDITION NUMBER
C           OF THE MATRIX G
C
C SCRATCH STORAGE ALLOCATED - N DOUBLE PRECISION LOCATIONS
C ERROR CONDITIONS -
C       1    N.LT.1     FATAL
C       2    ML.LT.1    FATAL
C       3    IG.LT.ML   FATAL
      INTEGER N,ML,IG
      DOUBLE PRECISION G(IG,N), COND, GNORM,DBPNM,D1MACH
      COMMON /CSTAK/ D
      DOUBLE PRECISION D(500)
      DOUBLE PRECISION R(500)
      EQUIVALENCE (R(1), D(1))
      CALL ENTER(1)
C/6S
C     IF (N.LT.1)  CALL SETERR(13H DBPCE-N.LT.1, 13, 1, 2)
C     IF (ML.LT.1) CALL SETERR(14H DBPCE-ML.LT.1, 14, 2, 2)
C     IF (IG.LT.ML)CALL SETERR(15H DBPCE-IG.LT.ML, 15, 3, 2)
C/7S
      IF (N.LT.1)  CALL SETERR(' DBPCE-N.LT.1', 13, 1, 2)
      IF (ML.LT.1) CALL SETERR(' DBPCE-ML.LT.1', 14, 2, 2)
      IF (IG.LT.ML)CALL SETERR(' DBPCE-IG.LT.ML', 15, 3, 2)
C/
      GNORM=DBPNM(N,ML,G,IG)
      CALL DBPLD(N,ML,G,IG,0.D0)
      IF(NERROR(NERR).EQ.0) GOTO 20
      IF(NERROR(NERR).LT.10+N) GO TO 10
C/6S
C     CALL N5ERR(30H DBPCE-MATRIX NOT POSITIVE DEF,30,NERR,1)
C/7S
      CALL N5ERR(' DBPCE-MATRIX NOT POSITIVE DEF',30,NERR,1)
C/
      GO TO 30
 10   COND = D1MACH(2)
C/6S
C     CALL N5ERR(22H DBPCE-SINGULAR MATRIX,22,NERR,1)
C/7S
      CALL N5ERR(' DBPCE-SINGULAR MATRIX',22,NERR,1)
C/
      GO TO 30
 20   IWORK = ISTKGT(N,4)
      CALL DB4PCE(N,ML,G,IG,GNORM,COND,R(IWORK))
 30   CALL LEAVE
      RETURN
      END
