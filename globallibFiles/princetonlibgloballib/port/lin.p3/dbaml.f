         SUBROUTINE DBAML(N,ML,M,A,IA,X,Y)
C
C MULTIPLICATION OF VECTOR X BY BAND MATRIX B
C
C INPUT PARAMETERS
C N    ORDER OF MATRIX
C ML    NUMBER OF DIAGONALS ON AND BELOW DIAGONAL OF B
C M     NUMBER OF DIAGONALS OF B
C A    ARRAY INTO WHICH A BAND MATRIX IS PACKED
C      A(ML+J-I,I)= (I,J)TH ELEMENT OF BAND MATRIX
C IA   ROW DIMENSION OF A
C X    VECTOR TO BE MULTIPLIED
C
C OUTPUT PARAMETERS
C Y    BX, WHERE B IS THE BAND MATRIX
C ERROR CONDITIONS
C N.LT.1       FATAL
C ML.LT.1      FATAL
C M.LT.ML      FATAL
C IA.LT.M      FATAL
         DOUBLE PRECISION A(IA,N),X(M),Y(N),SUM
C/6S
C        IF(N.LT.1) CALL SETERR(12HDBAML-N.LT.1,12,1,2)
C        IF(ML.LT.1) CALL SETERR(13HDBAML-ML.LT.1,13,2,2)
C        IF(M.LT.ML) CALL SETERR(13HDBAML-M.LT.ML,13,3,2)
C        IF (IA.LT.M) CALL SETERR(13HDBAML-IG.LT.M,13,4,2)
C/7S
         IF(N.LT.1) CALL SETERR('DBAML-N.LT.1',12,1,2)
         IF(ML.LT.1) CALL SETERR('DBAML-ML.LT.1',13,2,2)
         IF(M.LT.ML) CALL SETERR('DBAML-M.LT.ML',13,3,2)
         IF (IA.LT.M) CALL SETERR('DBAML-IG.LT.M',13,4,2)
C/
         JB=ML+1
         JE=M
         JEND=N-(M-ML)
         IS=1
         DO 10 I=1,N
            JB=MAX0(1,JB-1)
            IX=IS
            SUM=0.D0
            DO 5 J=JB,JE
               SUM=SUM+A(J,I)*X(IX)
               IX=IX+1
 5           CONTINUE
            Y(I)=SUM
            IF(I.GE.JEND)JE=JE-1
            IF(JB.EQ.1)IS=IS+1
 10       CONTINUE
          RETURN
          END
